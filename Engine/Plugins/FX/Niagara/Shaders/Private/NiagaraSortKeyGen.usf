// Copyright Epic Games, Inc. All Rights Reserved.

/*==============================================================================
	NiagaraSortKeyGen.usf: Shader to generate particle sorting keys.
==============================================================================*/

/*------------------------------------------------------------------------------
	Compile time parameters:
		THREAD_COUNT - The number of threads to launch per workgroup.
		SORT_VIEW_DEPTH
		SORT_VIEW_DISTANCE
		SORT_CUSTOM_ASCENDING
		SORT_CUSTOM_DESCENDING
------------------------------------------------------------------------------*/

#include "/Engine/Private/Common.ush"

#define NIAGARA_KEYGEN 1
#include "NiagaraVFParticleAccess.usf"

#if NIAGARA_LOOSEPARAM_BINDING
Buffer<float> NiagaraParticleDataFloat;
#endif

Buffer<uint> GPUParticleCountBuffer;

// (ParticleCount, GPUParticleCountOffset)
uint2 ParticleCountParams;

// (EmitterKey, OutputOffset, SortMode, SortAttributeOffset)
uint4 SortParams;

float3 CameraPosition;
float3 CameraDirection;

/** Output key buffer. */
RWBuffer<uint> OutKeys;
/** Output indices buffer. */
RWBuffer<int> OutParticleIndices;

#if SORT_MAX_PRECISION
// (SortKeyMask, SortKeyShift, SortKeySignBit)
uint4 SortKeyParams;
#endif

[numthreads(THREAD_COUNT,1,1)]
void GenerateParticleSortKeys(uint ParticleIndex : SV_DispatchThreadID)
{
	const uint ParticleCount = ParticleCountParams.x;
	const uint GPUParticleCountOffset = ParticleCountParams.y;

	const uint GPUParticleCount = GPUParticleCountOffset != -1 ? GPUParticleCountBuffer[GPUParticleCountOffset] : ParticleCount;

	const uint EmitterKey = SortParams.x;
	const uint OutputOffset = SortParams.y;
	const uint SortMode = SortParams.z;
	const uint SortAttributeOffset = SortParams.w;

#if SORT_MAX_PRECISION
	const uint SortKeyMask = SortKeyParams.x; 
	const uint SortKeyShift = SortKeyParams.y;
	const uint SortKeySignBit = SortKeyParams.z;
#else
	const uint SortKeyMask = 0xffff; 
	const uint SortKeySignBit = 0x8000;
#endif

	if (ParticleIndex < ParticleCount)
	{
		float SortValue = 0;
		uint SortKey = SortKeyMask; 

		if (ParticleIndex < GPUParticleCount)
		{
			if (SortMode == SORT_VIEW_DEPTH || SortMode == SORT_VIEW_DISTANCE)
			{ 
				float3 ParticlePosition = GetVec3(NIAGARA_LOOSEPARAM_STRUCT(NiagaraParticleDataFloat), SortAttributeOffset, ParticleIndex); 
				float3 CameraVector = ParticlePosition - CameraPosition;
				SortValue = SortMode == SORT_VIEW_DEPTH ? dot(CameraVector, CameraDirection) : length(CameraVector);
				
#if SORT_MAX_PRECISION
				SortKey = (asuint(SortValue)) >> SortKeyShift;
#else
				SortKey = f32tof16(SortValue);
#endif
				// Render far away first, knowing that SortValue > 0
				SortKey =  (~SortKey) & SortKeyMask;
			}
			else
			{
				SortValue = GetFloat(NIAGARA_LOOSEPARAM_STRUCT(NiagaraParticleDataFloat), SortAttributeOffset, ParticleIndex);

#if SORT_MAX_PRECISION
				SortKey = (asuint(SortValue)) >> SortKeyShift;
#else
				SortKey = f32tof16(SortValue);
#endif
				// Special handling for negative values.
				SortKey = SortValue >= 0 ? (SortKey | SortKeySignBit) : ~SortKey;

				SortKey = (SortMode == SORT_CUSTOM_ASCENDING ? SortKey : ~SortKey) & SortKeyMask;
			}

			// Clamp to max-1 to make sure that out-of-bound indices are at the end.
			SortKey = min(SortKey , SortKeyMask - 1);
		}

		SortKey |= EmitterKey;

		OutKeys[OutputOffset + ParticleIndex] = SortKey;
		OutParticleIndices[OutputOffset + ParticleIndex] = ParticleIndex;
	}
}
 
