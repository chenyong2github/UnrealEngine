// Copyright Epic Games, Inc. All Rights Reserved.

#ifndef __MATRIX33_ISPH__
#define __MATRIX33_ISPH__

#include "Math/Vector.isph"
#include "Math/Matrix.isph"

typedef FMatrix FMatrix33;

static const uniform struct FMatrix33 Matrix33Zero = {{ 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1}};

inline uniform FMatrix33 SetMatrix33(
	const uniform float x00, const uniform float x10, const uniform float x20, 
	const uniform float x01, const uniform float x11, const uniform float x21, 
	const uniform float x02, const uniform float x12, const uniform float x22)
{
	return SetMatrix(SetVector4(x00, x01, x02, 0), SetVector4(x10, x11, x12, 0), SetVector4(x20, x21, x22, 0), Float0001);
}

inline uniform FMatrix33 SetMatrix33(
	const uniform float x00, const uniform float x10, const uniform float x20, 
	const uniform float x11, const uniform float x21, const uniform float x22)
{
	return SetMatrix(SetVector4(x00, x10, x20, 0), SetVector4(x10, x11, x21, 0), SetVector4(x20, x21, x22, 0), Float0001);
}

inline uniform FMatrix33 SetMatrix33(
	const uniform float x00, const uniform float x11, const uniform float x22)
{
	return SetMatrix(SetVector4(x00, 0, 0, 0), SetVector4(0, x11, 0, 0), SetVector4(0, 0, x22, 0), Float0001);
}

inline uniform FMatrix33 SetMatrix33(const uniform FVector4& x0, const uniform FVector4& x1, const uniform FVector4& x2)
{
	return SetMatrix(x0, x1, x2, Float0001);
}

inline uniform FMatrix33 MultiplyAB(const uniform FMatrix33& L, const uniform FMatrix33& R)
{
	const uniform FVector4 L0 = *((uniform FVector4 *uniform)&L.M[0]);
	const uniform FVector4 L1 = *((uniform FVector4 *uniform)&L.M[4]);
	const uniform FVector4 L2 = *((uniform FVector4 *uniform)&L.M[8]);

	uniform FVector4 x0 = SetVector4(R.M[0]) * L0 + SetVector4(R.M[1]) * L1 + SetVector4(R.M[2]) * L2;
	uniform FVector4 x1 = SetVector4(R.M[4]) * L0 + SetVector4(R.M[5]) * L1 + SetVector4(R.M[6]) * L2;
	uniform FVector4 x2 = SetVector4(R.M[8]) * L0 + SetVector4(R.M[9]) * L1 + SetVector4(R.M[10]) * L2;

	x0.V[3] = 0;
	x1.V[3] = 0;
	x2.V[3] = 0;

	return SetMatrix33(x0, x1, x2);
}

inline uniform FMatrix33 MultiplyABt(const uniform FMatrix33& L, const uniform FMatrix33& R)
{
	const uniform FVector4 L0 = *((uniform FVector4 *uniform)&L.M[0]);
	const uniform FVector4 L1 = *((uniform FVector4 *uniform)&L.M[4]);
	const uniform FVector4 L2 = *((uniform FVector4 *uniform)&L.M[8]);

	uniform FVector4 x0 = SetVector4(R.M[0]) * L0 + SetVector4(R.M[4]) * L1 + SetVector4(R.M[8]) * L2;
	uniform FVector4 x1 = SetVector4(R.M[1]) * L0 + SetVector4(R.M[5]) * L1 + SetVector4(R.M[9]) * L2;
	uniform FVector4 x2 = SetVector4(R.M[2]) * L0 + SetVector4(R.M[6]) * L1 + SetVector4(R.M[10]) * L2;

	x0.V[3] = 0;
	x1.V[3] = 0;
	x2.V[3] = 0;

	return SetMatrix33(x0, x1, x2);
}

inline uniform FVector Multiply(const uniform FMatrix33& LIn, const uniform FVector& R)
{
	const uniform FVector4 L0 = *((uniform FVector4 *uniform)&LIn.M[0]);
	const uniform FVector4 L1 = *((uniform FVector4 *uniform)&LIn.M[4]);
	const uniform FVector4 L2 = *((uniform FVector4 *uniform)&LIn.M[8]);

	const uniform FVector4 Result = L0 * SetVector4(R.V[0]) + L1 * SetVector4(R.V[1]) + L2 * SetVector4(R.V[2]);

	return SetVector(Result.V[0], Result.V[1], Result.V[2]);
}

inline uniform FMatrix33 AddAB(const uniform FMatrix33& L, const uniform FMatrix33& R)
{
	const uniform FVector4 L0 = *((uniform FVector4 *uniform)&L.M[0]);
	const uniform FVector4 L1 = *((uniform FVector4 *uniform)&L.M[4]);
	const uniform FVector4 L2 = *((uniform FVector4 *uniform)&L.M[8]);

	const uniform FVector4 R0 = *((uniform FVector4 *uniform)&R.M[0]);
	const uniform FVector4 R1 = *((uniform FVector4 *uniform)&R.M[4]);
	const uniform FVector4 R2 = *((uniform FVector4 *uniform)&R.M[8]);

	uniform FVector4 x0 = R0 + L0;
	uniform FVector4 x1 = R1 + L1;
	uniform FVector4 x2 = R2 + L2;

	x0.V[3] = 0;
	x1.V[3] = 0;
	x2.V[3] = 0;

	return SetMatrix33(x0, x1, x2);
}

inline uniform FMatrix33 MakeQuatRotationTranslationMatrix(const uniform FVector4& Q)
{
	// Assumes origin is 0,0,0
	const uniform FVector4 xyz2 = Q + Q; // x2, y2, z2
	const uniform FVector4 x = xyz2 * Q.V[0]; // xx, xy, xz
	const uniform FVector4 y = xyz2 * Q.V[1]; // yx, yy, yz
	const uniform FVector4 w = xyz2 * Q.V[3]; // wx, wy, wz
	const uniform float zz = Q.V[2] * xyz2.V[2];

	const uniform FVector4 yx = SetVector4(y.V[2], x.V[2], x.V[1], 0); // yz, xz, xy
	const uniform FVector4 Adds = yx + w; // M[6], M[8], M[1]
	const uniform FVector4 Minuses = yx - w; // M[9], M[2], M[4]
	
	const uniform FVector4 OneMinus0 = SetVector4(y.V[1], x.V[0], x.V[0], 0); // yy, xx, xx
	const uniform FVector4 Oneminus1 = SetVector4(zz, zz, y.V[1], 0); // zz, zz, yy
	const uniform FVector4 OneMinuses = FloatOne - (OneMinus0 + Oneminus1); // M[0], M[5], M[10]
	
	return SetMatrix33(
		SetVector4(OneMinuses.V[0], Adds.V[2], Minuses.V[1], 0),
		SetVector4(Minuses.V[2], OneMinuses.V[1], Adds.V[0], 0),
		SetVector4(Adds.V[1], Minuses.V[0], OneMinuses.V[2], 0)
	);
}

#endif
