// Copyright Epic Games, Inc. All Rights Reserved.

/*=============================================================================
	LumenVoxelLighting.usf
=============================================================================*/

#include "../Common.ush"
#include "LumenCardCommon.ush"

// 16 bit render target accumulation
#define DEPTH_WEIGHTED_OIT_32_BIT 0
// Faster to always trace highest resolution mip for short voxelization traces, there's rarely empty space to skip
#define SDF_TRACING_TRAVERSE_MIPS 0
// Pick best sample instead of using stochastic
#define SURFACE_CACHE_SAMPLING_METHOD SURFACE_CACHE_SAMPLING_METHOD_BEST_SAMPLE
#include "LumenTracingCommon.ush"

#ifndef THREADGROUP_SIZE
	#define THREADGROUP_SIZE 1
#endif

#define NUM_VOXEL_DIRECTIONS 6
#define VIS_BUFFER_TILE_SIZE 4
#define VIS_BUFFER_SHADING_WAVE_SIZE 64

#define HIT_OBJECT_TYPE_MESH_SDF 0
#define HIT_OBJECT_TYPE_HEIGHTFIELD 1

struct FVoxelVisBuffer
{
	float NormalizedHitDistance;
	uint HitObjectId;
	uint HitObjectType;
};

#define VIS_BUFFER_INVALID	0
#define VIS_BUFFER_CLEAR	0xFFFFFFFF

uint EncodeVoxelVisBuffer(FVoxelVisBuffer VisBuffer)
{
	uint HitDistanceUInt = uint(saturate(VisBuffer.NormalizedHitDistance) * 0xFF) & 0xFF;
	uint HitObjectTypeUInt = VisBuffer.HitObjectType & 0x01;
	uint HitObjectUInt = VisBuffer.HitObjectId & 0x7FFFFF;

	uint Payload = (HitDistanceUInt << 24) | HitObjectTypeUInt << 23 | HitObjectUInt;

	// Don't collide with VIS_BUFFER_INVALID
	Payload = max(Payload, 1U);

	return Payload;
}

FVoxelVisBuffer DecodeVoxelVisBuffer(uint Payload)
{
	FVoxelVisBuffer VisBuffer;
	VisBuffer.NormalizedHitDistance = ((Payload >> 24) & 0xFF) / float(0xFF);
	VisBuffer.HitObjectType = (Payload >> 23) & 0x01;
	VisBuffer.HitObjectId = Payload & 0x7FFFFF;

	return VisBuffer;
}

uint3 ClipmapGridResolution;
float3 VoxelCoordToUVScale;
float3 VoxelCoordToUVBias;

uint VoxelCoordToVisBufferCoord(uint ClipmapIndex, uint3 VoxelCoord, uint DirectionIndex)
{
	float3 ClipmapUV = frac((VoxelCoord + 0.5f) * VoxelCoordToUVScale + VoxelCoordToUVBias);

	uint3 TextureCoord = clamp(saturate(ClipmapUV) * ClipmapGridResolution, 0, ClipmapGridResolution - 1);
	TextureCoord.y += ClipmapIndex * ClipmapGridResolution.y;
	TextureCoord.z += DirectionIndex * ClipmapGridResolution.z;

	uint3 Dimension = uint3(ClipmapGridResolution.x, 0, ClipmapGridResolution.z * NUM_VOXEL_DIRECTIONS);
	uint3 GroupMaxCount = (Dimension + VIS_BUFFER_TILE_SIZE - 1) / VIS_BUFFER_TILE_SIZE;
	uint3 GroupThreadId = TextureCoord % VIS_BUFFER_TILE_SIZE;
	uint3 GroupId = TextureCoord / VIS_BUFFER_TILE_SIZE;

	uint GroupIndex = GroupId.x + GroupId.z * GroupMaxCount.x + GroupId.y * (GroupMaxCount.x * GroupMaxCount.z);
	uint GroupThreadIndex = ZOrder3DEncode(GroupThreadId, log2(VIS_BUFFER_TILE_SIZE));

	return GroupIndex * (VIS_BUFFER_TILE_SIZE * VIS_BUFFER_TILE_SIZE * VIS_BUFFER_TILE_SIZE) + GroupThreadIndex;
}

RWBuffer<uint> RWClearVisBufferIndirectArgBuffer;
RWBuffer<uint> RWTraceSetupIndirectArgBuffer;
RWBuffer<uint> RWTraceIndirectArgBuffer;

[numthreads(1, 1, 1)]
void ClearIndirectArgBuffersCS(
	uint3 GroupId : SV_GroupID,
	uint3 DispatchThreadId : SV_DispatchThreadID,
	uint3 GroupThreadId : SV_GroupThreadID)
{
	if (DispatchThreadId.x == 0)
	{
		RWClearVisBufferIndirectArgBuffer[0] = 0;
		RWClearVisBufferIndirectArgBuffer[1] = 1;
		RWClearVisBufferIndirectArgBuffer[2] = 1;

		RWTraceSetupIndirectArgBuffer[0] = 0;
		RWTraceSetupIndirectArgBuffer[1] = 1;
		RWTraceSetupIndirectArgBuffer[2] = 1;

		RWTraceIndirectArgBuffer[0] = 0;
		RWTraceIndirectArgBuffer[1] = 1;
		RWTraceIndirectArgBuffer[2] = 1;
	}
}

RWStructuredBuffer<uint> RWGridTileBuffer;
RWTexture3D<uint> RWGridTileMaskTexture;

Buffer<float4> UpdateBoundsBuffer;
uint NumUpdateBounds;

uint3 GridResolution;
float3 GridCoordToWorldCenterScale;
float3 GridCoordToWorldCenterBias;
float3 TileWorldExtent;

groupshared uint GroupInsideBounds;

[numthreads(THREADGROUP_SIZE, 1, 1)]
void BuildUpdateGridTilesCS(
	uint3 GroupId : SV_GroupID,
	uint3 DispatchThreadId : SV_DispatchThreadID,
	uint3 GroupThreadId : SV_GroupThreadID)
{
	uint3 GridCoord = GroupId.xyz;
	uint ThreadId = GroupThreadId.x;
	float3 GridWorldCenter = GridCoord * GridCoordToWorldCenterScale + GridCoordToWorldCenterBias;

	if (ThreadId == 0)
	{
		GroupInsideBounds = 0;
	}

	GroupMemoryBarrierWithGroupSync();

	LOOP
	for (uint UpdateBoundsIndex = ThreadId; UpdateBoundsIndex < NumUpdateBounds; UpdateBoundsIndex += THREADGROUP_SIZE)
	{
		float3 UpdateBoundsCenter = UpdateBoundsBuffer[UpdateBoundsIndex * 2 + 0].xyz;
		float3 UpdateBoundsExtent = UpdateBoundsBuffer[UpdateBoundsIndex * 2 + 1].xyz;

		float DistanceSq = ComputeSquaredDistanceBetweenAABBs(UpdateBoundsCenter, UpdateBoundsExtent, GridWorldCenter, TileWorldExtent);
		if (DistanceSq <= 0.0f)
		{
			// Metal compiler issue: it requires `+=` instead of `=` to record the value as of UE 5.0
			GroupInsideBounds += 1;
			break;
		}
	}

	GroupMemoryBarrierWithGroupSync();

	if (all(GridCoord < GridResolution) && ThreadId == 0)
	{
		if (GroupInsideBounds)
		{
			uint DestIndex;
			InterlockedAdd(RWClearVisBufferIndirectArgBuffer[0], 1, DestIndex);
			RWGridTileBuffer[DestIndex] = GridCoord.x | (GridCoord.y << 8) | (GridCoord.z << 16);
		}

		RWGridTileMaskTexture[GridCoord] = GroupInsideBounds;
	}
}

RWStructuredBuffer<uint> RWVoxelVisBuffer;
StructuredBuffer<uint> UpdateTileBuffer;
uint ClipmapIndex;

[numthreads(VIS_BUFFER_TILE_SIZE, VIS_BUFFER_TILE_SIZE, VIS_BUFFER_TILE_SIZE * NUM_VOXEL_DIRECTIONS)]
void ClearVisBufferCS(
	uint3 GroupId : SV_GroupID,
	uint3 DispatchThreadId : SV_DispatchThreadID,
	uint3 GroupThreadId : SV_GroupThreadID)
{
	uint UpdateTileIndex = GroupId.x;
	uint PackedUpdateTile = UpdateTileBuffer[UpdateTileIndex];

	uint3 UpdateTileCoord;
	UpdateTileCoord.x = PackedUpdateTile & 0xFF;
	UpdateTileCoord.y = (PackedUpdateTile >> 8) & 0xFF;
	UpdateTileCoord.z = (PackedUpdateTile >> 16) & 0xFF;

	uint3 VoxelCoordOffset;
	VoxelCoordOffset.x = GroupThreadId.x;
	VoxelCoordOffset.y = GroupThreadId.y;
	VoxelCoordOffset.z = GroupThreadId.z % VIS_BUFFER_TILE_SIZE;

	uint DirectionIndex = GroupThreadId.z / VIS_BUFFER_TILE_SIZE;

	uint3 VoxelCoord = UpdateTileCoord * VIS_BUFFER_TILE_SIZE + VoxelCoordOffset;

	uint VisBufferCoord = VoxelCoordToVisBufferCoord(ClipmapIndex, VoxelCoord, DirectionIndex);
	RWVoxelVisBuffer[VisBufferCoord] = VIS_BUFFER_CLEAR;
}

RWStructuredBuffer<uint> RWObjectIndexBuffer;
float3 VoxelClipmapWorldCenter;
float3 VoxelClipmapWorldExtent;
float MeshRadiusThreshold;

#define SDF_MESH_TYPE 0
#define HEIGHTFIELD_MESH_TYPE 1

[numthreads(THREADGROUP_SIZE, 1, 1)]
void CullToVoxelClipmapCS(
	uint3 GroupId : SV_GroupID,
	uint3 DispatchThreadId : SV_DispatchThreadID,
	uint3 GroupThreadId : SV_GroupThreadID)
{
	uint ObjectIndex = DispatchThreadId.x;

	float3 WorldObjectCenter = float3(0, 0, 0);
	float3 WorldObjectExtent = float3(0, 0, 0);
	bool bEmissiveLightSource = false;

	#if MESH_TYPE == SDF_MESH_TYPE
	{
		if (ObjectIndex < NumSceneObjects)
		{
			FDFObjectBounds DFObjectBounds = LoadDFObjectBounds(ObjectIndex);
			WorldObjectCenter = LWCHackToFloat(DFObjectBounds.Center);
			WorldObjectExtent = DFObjectBounds.BoxExtent;
			bEmissiveLightSource = DFObjectBounds.bEmissiveLightSource;
		}
	}
	#else // MESH_TYPE == HEIGHTFIELD_MESH_TYPE
	{
		if (ObjectIndex < LumenCardScene.NumHeightfields)
		{
			FLumenHeightfieldData LumenHeightfield = GetLumenHeightfieldData(ObjectIndex);
			if (LumenHeightfield.bValid)
			{
				WorldObjectCenter = LumenHeightfield.BoundsCenter;
				WorldObjectExtent = LumenHeightfield.BoundsExtent;
			}
		}
	}
	#endif

	float BoundsRadius = length(WorldObjectExtent);

	BRANCH
	if (BoundsRadius > 0.0f && (BoundsRadius > MeshRadiusThreshold || bEmissiveLightSource))
	{
		float DistanceSq = ComputeSquaredDistanceBetweenAABBs(VoxelClipmapWorldCenter, VoxelClipmapWorldExtent, WorldObjectCenter, WorldObjectExtent);
		if (DistanceSq <= 0.0f)
		{
			uint DestIndex;
			InterlockedAdd(RWTraceSetupIndirectArgBuffer[0], 1, DestIndex);
			RWObjectIndexBuffer[DestIndex] = ObjectIndex;
		}
	}
}

RWStructuredBuffer<uint> RWVoxelTraceAllocator;
RWStructuredBuffer<uint2> RWVoxelTraceData;
StructuredBuffer<uint> ObjectIndexBuffer;
StructuredBuffer<uint> CullGridTileBuffer;

float3 ConservativeRasterizationExtent;
float3 ClipmapToUpdateGridScale;
float3 ClipmapToUpdateGridBias;
int3 UpdateGridResolution;

Texture3D<uint> UpdateTileMaskTexture;

[numthreads(THREADGROUP_SIZE, THREADGROUP_SIZE, THREADGROUP_SIZE)]
void SetupVoxelTracesCS(
	uint3 GroupId : SV_GroupID,
	uint3 DispatchThreadId : SV_DispatchThreadID,
	uint3 GroupThreadId : SV_GroupThreadID)
{
	uint IndexInObjectIndexBuffer = GroupId.x;
	int3 UpdateGridBaseOffset = GroupThreadId.xyz * VIS_BUFFER_TILE_SIZE;
	uint ObjectIndex = ObjectIndexBuffer[IndexInObjectIndexBuffer];

#if MESH_TYPE == SDF_MESH_TYPE
	uint NumMeshObjects = NumSceneObjects;
#else // MESH_TYPE == HEIGHTFIELD_MESH_TYPE
	uint NumMeshObjects = LumenCardScene.NumHeightfields;
#endif // MESH_TYPE
	if (ObjectIndex >= NumMeshObjects)
	{
		return;
	}

#if MESH_TYPE == SDF_MESH_TYPE
	FDFObjectBounds DFObjectBounds = LoadDFObjectBounds(ObjectIndex);
	float3 ObjectCenter = LWCHackToFloat(DFObjectBounds.Center);
	float3 ObjectExtent = DFObjectBounds.BoxExtent;

#else // MESH_TYPE == HEIGHTFIELD_MESH_TYPE

	FLumenHeightfieldData LumenHeightfield = GetLumenHeightfieldData(ObjectIndex);
	FLumenMeshCardsData MeshCardsData = GetLumenMeshCardsData(LumenHeightfield.MeshCardsIndex);

	float3 ObjectCenter = LumenHeightfield.BoundsCenter;
	float3 ObjectExtent = LumenHeightfield.BoundsExtent;
#endif

	int3 ObjectGridMin = (ObjectCenter - ObjectExtent - ConservativeRasterizationExtent) * ClipmapToUpdateGridScale + ClipmapToUpdateGridBias;
	int3 ObjectGridMax = (ObjectCenter + ObjectExtent + ConservativeRasterizationExtent) * ClipmapToUpdateGridScale + ClipmapToUpdateGridBias;

	ObjectGridMin = clamp(ObjectGridMin, UpdateGridBaseOffset, UpdateGridBaseOffset + VIS_BUFFER_TILE_SIZE);
	ObjectGridMax = clamp(ObjectGridMax, UpdateGridBaseOffset, UpdateGridBaseOffset + VIS_BUFFER_TILE_SIZE);

	if (all(ObjectGridMax > ObjectGridMin))
	{
		for (int PosZ = ObjectGridMin.z; PosZ < ObjectGridMax.z; ++PosZ)
		{
			for (int PosY = ObjectGridMin.y; PosY < ObjectGridMax.y; ++PosY)
			{
				for (int PosX = ObjectGridMin.x; PosX < ObjectGridMax.x; ++PosX)
				{
					const uint3 TileCoord = uint3(PosX, PosY, PosZ);
					if (UpdateTileMaskTexture[TileCoord] > 0)
					{
						const uint3 TileCoord = uint3(PosX, PosY, PosZ);

						uint WriteIndex;
						InterlockedAdd(RWTraceIndirectArgBuffer[0], 1, WriteIndex);

						RWVoxelTraceData[WriteIndex].x = TileCoord.x | (TileCoord.y << 8) | (TileCoord.z << 16);
						RWVoxelTraceData[WriteIndex].y = ObjectIndex;
					}
				}
			}
		}
	}
}

StructuredBuffer<uint2> VoxelTraceData;
float3 GridMin;
float3 GridVoxelSize;

[numthreads(THREADGROUP_SIZE, 1, 1)]
void VoxelTraceCS(
	uint3 GroupId : SV_GroupID,
	uint3 DispatchThreadId : SV_DispatchThreadID,
	uint3 GroupThreadId : SV_GroupThreadID)
{
	uint ThreadIndex = GroupThreadId.x;

	uint VoxelTraceIndex = GroupId.x;
	uint PackedTileCoord = VoxelTraceData[VoxelTraceIndex].x;
	uint ObjectIndex = VoxelTraceData[VoxelTraceIndex].y;

	uint3 TileCoord;
	TileCoord.x = PackedTileCoord & 0xFF;
	TileCoord.y = (PackedTileCoord >> 8) & 0xFF;
	TileCoord.z = (PackedTileCoord >> 16) & 0xFF;

	for (uint TraceIndex = ThreadIndex; TraceIndex < 64 * NUM_VOXEL_DIRECTIONS; TraceIndex += 64)
	{
		uint DirectionIndex = TraceIndex % NUM_VOXEL_DIRECTIONS;
		uint VoxelIndex = TraceIndex / NUM_VOXEL_DIRECTIONS;

		uint3 VoxelCoord = TileCoord * VIS_BUFFER_TILE_SIZE;
		VoxelCoord.x += VoxelIndex % VIS_BUFFER_TILE_SIZE;
		VoxelCoord.y += (VoxelIndex / VIS_BUFFER_TILE_SIZE) % VIS_BUFFER_TILE_SIZE;
		VoxelCoord.z += VoxelIndex / (VIS_BUFFER_TILE_SIZE * VIS_BUFFER_TILE_SIZE);

		float3 VoxelMin = GridMin + GridVoxelSize * VoxelCoord;
		float3 VoxelExtent = .5f * GridVoxelSize;
		float3 VoxelCenter = VoxelMin + VoxelExtent;

		float Sign = (DirectionIndex & 1) ? -1.0f : 1.0f;
		uint Axis = DirectionIndex / 2;
		float3 AxisDirection = Sign * float3(Axis == 0 ? 1.0f : 0.0f, Axis == 1 ? 1.0f : 0.0f, Axis == 2 ? 1.0f : 0.0f);
		float3 RayStart = VoxelCenter - AxisDirection * VoxelExtent;
		float3 RayEnd = RayStart + AxisDirection * 2 * VoxelExtent;
		float RayLength = length(RayEnd - RayStart);

		FConeTraceInput TraceInput;
		TraceInput.Setup(RayStart, AxisDirection, 0, 0, 0, RayLength, 2.0f);
		TraceInput.bUseEpsilonTraceForHeightfields = false;
		TraceInput.ConeStartRadius = length(AxisDirection * VoxelExtent);

		FTraceMeshSDFResult TraceMeshSDFResult;
		TraceMeshSDFResult.HitDistance = TraceInput.MaxTraceDistance;
		TraceMeshSDFResult.HitObject = 0;

		RayTraceSingleMeshSDF(
			TraceInput.ConeOrigin,
			TraceInput.ConeDirection,
			TraceInput.TanConeAngle,
			TraceInput.MinTraceDistance,
			TraceInput.MaxTraceDistance,
			ObjectIndex,
			true,
			0.0f,
			false, 
			float2(0, 0),
			TraceMeshSDFResult);

		if (TraceMeshSDFResult.HitDistance < TraceInput.MaxTraceDistance)
		{
			uint VisBufferPayload = VIS_BUFFER_CLEAR;

			if (TraceMeshSDFResult.bRayStartInsideGeometry)
			{
				VisBufferPayload = VIS_BUFFER_INVALID;
			}
			else
			{
				FVoxelVisBuffer VisBuffer;
				VisBuffer.NormalizedHitDistance = saturate(TraceMeshSDFResult.HitDistance / TraceInput.MaxTraceDistance);
				VisBuffer.HitObjectType = HIT_OBJECT_TYPE_MESH_SDF;
				VisBuffer.HitObjectId = TraceMeshSDFResult.HitObject;
				VisBufferPayload = EncodeVoxelVisBuffer(VisBuffer);
			}

			uint VisBufferCoord = VoxelCoordToVisBufferCoord(ClipmapIndex, VoxelCoord, DirectionIndex);
			InterlockedMin(RWVoxelVisBuffer[VisBufferCoord], VisBufferPayload);
		}
	}
}

[numthreads(THREADGROUP_SIZE, 1, 1)]
void HeightfieldVoxelTraceCS(
	uint3 GroupId : SV_GroupID,
	uint3 DispatchThreadId : SV_DispatchThreadID,
	uint3 GroupThreadId : SV_GroupThreadID)
{
	uint ThreadIndex = GroupThreadId.x;

	uint VoxelTraceIndex = GroupId.x;
	uint PackedTileCoord = VoxelTraceData[VoxelTraceIndex].x;
	uint HeightfieldObjectIndex = VoxelTraceData[VoxelTraceIndex].y;

	uint3 TileCoord;
	TileCoord.x = PackedTileCoord & 0xFF;
	TileCoord.y = (PackedTileCoord >> 8) & 0xFF;
	TileCoord.z = (PackedTileCoord >> 16) & 0xFF;

	for (uint TraceIndex = ThreadIndex; TraceIndex < 64 * NUM_VOXEL_DIRECTIONS; TraceIndex += 64)
	{
		// VoxelIndex write affinity
		uint DirectionIndex = TraceIndex % NUM_VOXEL_DIRECTIONS;
		uint VoxelIndex = TraceIndex / NUM_VOXEL_DIRECTIONS;
		// DirectionIndex trace affinity
		//uint DirectionIndex = TraceIndex / 64;
		//uint VoxelIndex = TraceIndex % 64;

		uint3 VoxelCoord = TileCoord * VIS_BUFFER_TILE_SIZE;
		VoxelCoord.x += VoxelIndex % VIS_BUFFER_TILE_SIZE;
		VoxelCoord.y += (VoxelIndex / VIS_BUFFER_TILE_SIZE) % VIS_BUFFER_TILE_SIZE;
		VoxelCoord.z += VoxelIndex / (VIS_BUFFER_TILE_SIZE * VIS_BUFFER_TILE_SIZE);

		float3 VoxelMin = GridMin + GridVoxelSize * VoxelCoord;
		float3 VoxelExtent = .5f * GridVoxelSize;
		float3 VoxelCenter = VoxelMin + VoxelExtent;

		float Sign = (DirectionIndex & 1) ? -1.0f : 1.0f;
		uint Axis = DirectionIndex / 2;
		float3 AxisDirection = Sign * float3(Axis == 0 ? 1.0f : 0.0f, Axis == 1 ? 1.0f : 0.0f, Axis == 2 ? 1.0f : 0.0f);
		float3 RayStart = VoxelCenter - AxisDirection * VoxelExtent;
		float3 RayEnd = RayStart + AxisDirection * 2 * VoxelExtent;

		FConeTraceInput TraceInput;
		float ConeAngle = 0.0f;
		float MinSampleRadius = length(AxisDirection * VoxelExtent);
		float MinTraceDistance = 0.0f;
		float MaxTraceDistance = length(RayEnd - RayStart);
		float StepFactor = 0.25f; // 0.25 of a voxel
		TraceInput.Setup(RayStart, AxisDirection, ConeAngle, MinSampleRadius, MinTraceDistance, MaxTraceDistance, StepFactor);
		TraceInput.bUseEpsilonTraceForHeightfields = false;

		// Trace Heightfield object
		FConeTraceHeightfieldSimpleResult Result = ConeTraceHeightfieldSimple(TraceInput, HeightfieldObjectIndex);

		if (Result.bIsHit)
		{
			FVoxelVisBuffer VisBuffer;
			VisBuffer.NormalizedHitDistance = saturate(Result.HitDistance / TraceInput.MaxTraceDistance);
			VisBuffer.HitObjectType = HIT_OBJECT_TYPE_HEIGHTFIELD;
			VisBuffer.HitObjectId = Result.bHitFrontFace ? HeightfieldObjectIndex : LUMEN_INVALID_HEIGHTFIELD_OBJECT_INDEX;
			uint VisBufferPayload = EncodeVoxelVisBuffer(VisBuffer);

			uint VisBufferCoord = VoxelCoordToVisBufferCoord(ClipmapIndex, VoxelCoord, DirectionIndex);
			InterlockedMin(RWVoxelVisBuffer[VisBufferCoord], VisBufferPayload);
		}
	}
}

RWStructuredBuffer<uint> RWCompactedVisBufferAllocator;
RWStructuredBuffer<uint> RWCompactedVisBuffer;

StructuredBuffer<uint> VoxelVisBuffer;

groupshared uint GroupCompactedVisBuffer[VIS_BUFFER_SHADING_WAVE_SIZE * NUM_VOXEL_DIRECTIONS];
groupshared uint GroupCompactedVisBufferAllocator;
groupshared uint GroupCompactedGlobalWriteOffset;

[numthreads(THREADGROUP_SIZE, THREADGROUP_SIZE, THREADGROUP_SIZE)]
void CompactVisBufferCS(
	uint3 GroupId : SV_GroupID,
	uint3 DispatchThreadId : SV_DispatchThreadID,
	uint3 GroupThreadId : SV_GroupThreadID)
{
	const uint LinearGroupThreadId = GroupThreadId.x + GroupThreadId.y * THREADGROUP_SIZE + GroupThreadId.z * THREADGROUP_SIZE * THREADGROUP_SIZE;
	if (LinearGroupThreadId == 0)
	{
		GroupCompactedVisBufferAllocator = 0;
	}

	GroupMemoryBarrierWithGroupSync();

	uint3 VoxelCoord = DispatchThreadId;
	if (all(VoxelCoord < ClipmapGridResolution))
	{
		for (uint DirectionIndex = 0; DirectionIndex < NUM_VOXEL_DIRECTIONS; ++DirectionIndex)
		{
			uint VisBufferCoord = VoxelCoordToVisBufferCoord(ClipmapIndex, VoxelCoord, DirectionIndex);
			uint VisBufferPayload = VoxelVisBuffer[VisBufferCoord];

			if (VisBufferPayload != VIS_BUFFER_INVALID && VisBufferPayload != VIS_BUFFER_CLEAR)
			{
				uint VisBufferIndex = 0;
				InterlockedAdd(GroupCompactedVisBufferAllocator, 1, VisBufferIndex);

				uint PackedVisBuffer = VoxelCoord.x | (VoxelCoord.y << 8) | (VoxelCoord.z << 16) | (DirectionIndex << 24);
				GroupCompactedVisBuffer[VisBufferIndex] = PackedVisBuffer;
			}
		}
	}

	GroupMemoryBarrierWithGroupSync();

	uint NumVisBufferShadingWaves = (GroupCompactedVisBufferAllocator + VIS_BUFFER_SHADING_WAVE_SIZE - 1) / VIS_BUFFER_SHADING_WAVE_SIZE;

	if (LinearGroupThreadId == 0 && NumVisBufferShadingWaves > 0)
	{
		InterlockedAdd(RWCompactedVisBufferAllocator[0], NumVisBufferShadingWaves, GroupCompactedGlobalWriteOffset);
	}

	GroupMemoryBarrierWithGroupSync();

	for (uint WaveIndex = 0; WaveIndex < NumVisBufferShadingWaves; ++WaveIndex)
	{
		uint VisBufferIndex = WaveIndex * VIS_BUFFER_SHADING_WAVE_SIZE + LinearGroupThreadId;
		RWCompactedVisBuffer[GroupCompactedGlobalWriteOffset * VIS_BUFFER_SHADING_WAVE_SIZE + VisBufferIndex] = VisBufferIndex < GroupCompactedVisBufferAllocator ? GroupCompactedVisBuffer[VisBufferIndex] : VIS_BUFFER_CLEAR;
	}
}

RWBuffer<uint3> RWCompactedVisBufferIndirectArguments;
StructuredBuffer<uint> CompactedVisBufferAllocator;

[numthreads(1, 1, 1)]
void SetupVisBufferShadingCS()
{
	RWCompactedVisBufferIndirectArguments[0] = CompactedVisBufferAllocator[0];
	RWCompactedVisBufferIndirectArguments[1] = 1;
	RWCompactedVisBufferIndirectArguments[2] = 1;
}

RWTexture3D<float4> RWVoxelLighting;
uint3 OutputGridResolution;

[numthreads(THREADGROUP_SIZE, THREADGROUP_SIZE, THREADGROUP_SIZE)]
void ClearVoxelLightingClipmapCS(
	uint3 GroupId : SV_GroupID,
	uint3 DispatchThreadId : SV_DispatchThreadID,
	uint3 GroupThreadId : SV_GroupThreadID)
{
	uint3 OutputCoord = DispatchThreadId;

	if (all(OutputCoord < OutputGridResolution))
	{
		uint3 VoxelLightingCoord = OutputCoord + uint3(0, ClipmapIndex * ClipmapGridResolution.y, 0);
		RWVoxelLighting[VoxelLightingCoord] = float4(0.0f, 0.0f, 0.0f, 0.0f);
	}
}

StructuredBuffer<uint> CompactedVisBuffer;

[numthreads(THREADGROUP_SIZE, 1, 1)]
void VisBufferShadingCS(
	uint3 GroupId : SV_GroupID,
	uint3 DispatchThreadId : SV_DispatchThreadID,
	uint3 GroupThreadId : SV_GroupThreadID)
{
	uint GlobalGroupIndex = GroupId.x;
	const uint LinearGroupThreadId = GroupThreadId.x + GroupThreadId.y * THREADGROUP_SIZE + GroupThreadId.z * THREADGROUP_SIZE * THREADGROUP_SIZE;

	if (GlobalGroupIndex < CompactedVisBufferAllocator[0])
	{
		uint CompactedVisBufferValue = CompactedVisBuffer[GlobalGroupIndex * THREADGROUP_SIZE + LinearGroupThreadId];
		if (CompactedVisBufferValue != VIS_BUFFER_CLEAR)
		{
			uint3 VoxelCoord;
			VoxelCoord.x = CompactedVisBufferValue & 0xFF;
			VoxelCoord.y = (CompactedVisBufferValue >> 8) & 0xFF;
			VoxelCoord.z = (CompactedVisBufferValue >> 16) & 0xFF;
			uint DirectionIndex = (CompactedVisBufferValue >> 24) & 0xFF;

			float3 VoxelMin = GridMin + GridVoxelSize * VoxelCoord;
			float3 VoxelExtent = .5f * GridVoxelSize;
			float3 VoxelCenter = VoxelMin + VoxelExtent;

			float3 Lighting = 0;
			float Opacity = 0;

			uint VisBufferCoord = VoxelCoordToVisBufferCoord(ClipmapIndex, VoxelCoord, DirectionIndex);
			uint VisBufferPayload = VoxelVisBuffer[VisBufferCoord];

			if (VisBufferPayload != VIS_BUFFER_INVALID && VisBufferPayload != VIS_BUFFER_CLEAR)
			{
				float Sign = (DirectionIndex & 1) ? -1.0f : 1.0f;
				uint Axis = DirectionIndex / 2;
				float3 RayDirection = Sign * float3(Axis == 0 ? 1.0f : 0.0f, Axis == 1 ? 1.0f : 0.0f, Axis == 2 ? 1.0f : 0.0f);
				float3 RayStart = VoxelCenter - RayDirection * VoxelExtent;
				float3 RayEnd = RayStart + RayDirection * 2 * VoxelExtent;
				float RayLength = length(RayEnd - RayStart);
				float ConeStartRadius = length(RayDirection * VoxelExtent);

				uint MeshCardsIndex = LUMEN_INVALID_MESH_CARDS_INDEX;
				float SurfaceCacheBias = 0.0f;

				FVoxelVisBuffer VisBuffer = DecodeVoxelVisBuffer(VisBufferPayload);
				float HitDistance = RayLength * VisBuffer.NormalizedHitDistance;

				if (VisBuffer.HitObjectType == HIT_OBJECT_TYPE_MESH_SDF)
				{
#if SHADE_MESH_SDF
					FTraceMeshSDFResult TraceMeshSDFResult;
					TraceMeshSDFResult.HitDistance = HitDistance;
					TraceMeshSDFResult.HitObject = VisBuffer.HitObjectId;

					FTraceMeshSDFDerivedData TraceSDFData = CalculateMeshSDFDerivedData(RayStart, RayDirection, RayLength, false, TraceMeshSDFResult);
					MeshCardsIndex = TraceSDFData.MeshCardsIndex;
					SurfaceCacheBias = TraceSDFData.SurfaceCacheBias;
#endif // SHADE_MESH_SDF
				}
				else if (VisBuffer.HitObjectType == HIT_OBJECT_TYPE_HEIGHTFIELD && VisBuffer.HitObjectId < LumenCardScene.NumHeightfields)
				{
#if SHADE_HEIGHTFIELD
					FLumenHeightfieldData LumenHeightfield = GetLumenHeightfieldData(VisBuffer.HitObjectId);
					MeshCardsIndex = LumenHeightfield.MeshCardsIndex;
					SurfaceCacheBias = 20.0f;
#endif // SHADE_HEIGHTFIELD
				}

				if (MeshCardsIndex < LumenCardScene.NumMeshCards)
				{
					float3 InterpolatePosition = RayStart + RayDirection * HitDistance;

					float InterpolateRadius = VoxelExtent.x;
					uint2 ScreenCoord = uint2(0, 0);
					bool bHiResSurface = false;
					uint AtlasId = FINAL_LIGHTING_ATLAS_ID;

					FSurfaceCacheSample SurfaceCacheSample = SampleLumenMeshCards(
						ScreenCoord,
						MeshCardsIndex,
						InterpolatePosition,
						-RayDirection,
						InterpolateRadius,
						SurfaceCacheBias,
						bHiResSurface,
						AtlasId);

					Lighting = SurfaceCacheSample.Lighting;
					Opacity = 1.0f;
				}

				uint3 WriteCoord = uint3(VoxelCoord.x, ClipmapIndex * ClipmapGridResolution.y + VoxelCoord.y, DirectionIndex * ClipmapGridResolution.z + VoxelCoord.z);
				RWVoxelLighting[WriteCoord] = float4(Lighting, Opacity);
			}
		}
	}
}
