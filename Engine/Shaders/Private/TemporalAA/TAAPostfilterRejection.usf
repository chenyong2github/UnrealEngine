// Copyright Epic Games, Inc. All Rights Reserved.

#include "TAACommon.ush"


//------------------------------------------------------- CONFIG

#define TILE_SIZE 8


//------------------------------------------------------- PARAMETERS

Texture2D<taa_half> HistoryRejectionTexture;
RWTexture2D<taa_half> HistoryRejectionOutput;


//------------------------------------------------------- FUNCTIONS

// Operator that output, lowest, median and highest values from 3 input values.
void LMHOperator(taa_half A, taa_half B, taa_half C, out taa_half L, out taa_half M, out taa_half H)
{
	// TODO: min3, max3, med3.
	taa_half X = min(B, C);
	taa_half Y = max(B, C);
	
	L = min(A, X);
	taa_half Z = max(A, X);

	M = min(Z, Y);
	H = max(Z, Y);
}

// 3 samples median.
taa_half Median(taa_half A, taa_half B, taa_half C)
{
	// TODO: med3.
	taa_half L, M, H;
	LMHOperator(A, B, C, L, M, H);
	return M;
}

// 9 samples median using Smith1996.
taa_half Median9(taa_half In[9])
{
	// First layer.
	taa_half L0[3];
	taa_half M0[3];
	taa_half H0[3];
	for (uint j = 0; j < 3; j++)
	{
		LMHOperator(In[j * 3 + 0], In[j * 3 + 1], In[j * 3 + 2], L0[j], M0[j], H0[j]);
	}
	
	// Second layer.
	// TODO: min3, max3.
	taa_half M1[3];
	M1[0] = max(max(L0[0], L0[1]), L0[2]);
	M1[1] = Median(M0[0], M0[1], M0[2]);
	M1[2] = min(min(H0[0], H0[1]), H0[2]);
	
	// Third layer.
	return Median(M1[0], M1[1], M1[2]);
}


//------------------------------------------------------- ENTRY POINT

[numthreads(TILE_SIZE, TILE_SIZE, 1)]
void MainCS(
	uint2 GroupId : SV_GroupID,
	uint GroupThreadIndex : SV_GroupIndex)
{
	float4 Debug = 0.0;

	uint2 DispatchThreadId = (
		ZOrder2D(GroupThreadIndex, log2(TILE_SIZE)) +
		GroupId * uint2(TILE_SIZE, TILE_SIZE));

	uint2 RejectionPixelPos = DispatchThreadId;

	float2 RejectionBufferUV = (RejectionPixelPos + 0.5) * RejectionInfo_ExtentInverse;

	// Fetch neighborhood of the kernel
	taa_half InSamples[9];
	UNROLL
	for (uint i = 0; i < 9; i++)
	{
		float2 Offset = float2(kOffsets3x3[i]);
		
		float2 SampleRejectionBufferUV = RejectionBufferUV + Offset * RejectionInfo_ExtentInverse;
		SampleRejectionBufferUV = clamp(SampleRejectionBufferUV, RejectionInfo_UVViewportBilinearMin, RejectionInfo_UVViewportBilinearMax);

		InSamples[i] = HistoryRejectionTexture.SampleLevel(GlobalPointClampedSampler, SampleRejectionBufferUV, 0);
	}

	taa_half Rejection = Median9(InSamples);

	if (all(RejectionPixelPos < RejectionInfo_ViewportSize))
	{
		HistoryRejectionOutput[RejectionPixelPos] = Rejection;

		#if DEBUG_OUTPUT
		{
			DebugOutput[RejectionPixelPos] = Debug;
		}
		#endif
	}
}
