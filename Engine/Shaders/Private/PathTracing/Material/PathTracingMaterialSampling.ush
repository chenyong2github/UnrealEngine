// Copyright Epic Games, Inc. All Rights Reserved.

/*=============================================================================================
PathTracingMaterialSampling.usf: Material sampling functions
===============================================================================================*/

#pragma once

#include "PathTracingMaterialCommon.ush"
#include "PathTracingFresnel.ush"
#include "PathTracingLambert.ush"
#if !SIMPLIFIED_MATERIAL_SHADER
#include "PathTracingGlossy.ush"
#if STRATA_ENABLED
#include "PathTracingSubstrate.ush"
#else
#include "PathTracingSolidGlass.ush"
#include "PathTracingThinGlass.ush"
#include "PathTracingDefaultLit.ush"
#include "PathTracingClearCoat.ush"
#include "PathTracingTwoSidedFoliage.ush"
#include "PathTracingCloth.ush"
#include "PathTracingSubsurfaceProfile.ush"
#endif // STRATA_ENABLED
#include "PathTracingEye.ush"
#include "PathTracingHair.ush"
#include "PathTracingMedium.ush"
#endif // !SIMPLIFIED_MATERIAL_SHADER

float3 EstimateMaterialAlbedo(FPathTracingPayload Payload)
{

	if (Payload.ShadingModelID == SHADINGMODELID_UNLIT)
	{
		// this object does not scatter light
		return 0;
	}
	// TODO: make this more accurate by estimating the directional albedo?
	float3 Albedo = 0;
	switch (Payload.ShadingModelID)
	{
		case SHADINGMODELID_UNLIT:
			return 0;
#if SIMPLIFIED_MATERIAL_SHADER
		case SHADINGMODELID_TWOSIDED_FOLIAGE:
			Albedo = Payload.BaseColor + Payload.SubsurfaceColor;
			break;
#elif PATHTRACING_STRATA_PAYLOAD
		case SHADINGMODELID_STRATA:
		{
			Albedo = Payload.DiffuseColor + Payload.SubsurfaceColor + Payload.SpecularColor + Payload.FuzzAmount * Payload.FuzzColor;
			Albedo *= Payload.GetMaxLobeWeight();
			break;
		}
		case SHADINGMODELID_EYE:
			Albedo = Payload.DiffuseColor + Payload.SubsurfaceColor + Payload.SpecularColor;
			break;
		case SHADINGMODELID_HAIR:
		// STRATA_TODO: Add more cases here?
#else
		case SHADINGMODELID_TWOSIDED_FOLIAGE:
		case SHADINGMODELID_SUBSURFACE:
		case SHADINGMODELID_PREINTEGRATED_SKIN:
		case SHADINGMODELID_SUBSURFACE_PROFILE:
		case SHADINGMODELID_EYE:
			Albedo = Payload.DiffuseColor + Payload.SpecularColor + Payload.SubsurfaceColor;
			break;
		case SHADINGMODELID_DEFAULT_LIT:
		case SHADINGMODELID_CLEAR_COAT:
			// TODO: include clearcoat albedo as well
			Albedo = Payload.DiffuseColor + Payload.SpecularColor;
			break;
		case SHADINGMODELID_THIN_TRANSLUCENT:
		{
			// glass portion is a mixture of pure specular and tinted transmission (when refracting)
			// or glass portion is just the specular highlight with transmission accounted for by transparency
			const float F0 = F0RGBToF0(Payload.SpecularColor);
			const float3 GlassAlbedo = Payload.HasRefraction() ? lerp(Payload.GetTransmittanceColor(), 1.0, F0) : F0;
			// surface is same as default-lit
			const float3 SurfAlbedo = Payload.DiffuseColor + Payload.SpecularColor;
			return lerp(GlassAlbedo, SurfAlbedo, Payload.BSDFOpacity);
		}
		case SHADINGMODELID_CLOTH:
			Albedo = (Payload.DiffuseColor + lerp(Payload.SpecularColor, Payload.GetClothColor(), Payload.GetClothAmount()));
			break;
		case SHADINGMODELID_HAIR:
#endif
		default:
			Albedo = Payload.GetBaseColor();
			break;
	}
	Albedo *= Payload.BSDFOpacity;

#if !SIMPLIFIED_MATERIAL_SHADER && !PATHTRACING_STRATA_PAYLOAD
	if (Payload.HasRefraction())
	{
		Albedo += 1 - Payload.BSDFOpacity; // glass albedo is 1.0
	}
#endif
	return Albedo;
}

#define ROUGHNESS_WEIGHT(r)		(saturate((r - max(MinRoughness, PathRoughness)) / (MaxRoughness - max(MinRoughness, PathRoughness))))

void AccumulateAlbedoNormal(inout FPathTracingPayload Payload, float3 PathThroughput, float PathRoughness, inout float3 Albedo, inout float3 Normal)
{
	const float MaxRoughness = 0.15; // don't bother past this level
	const float MinRoughness = 0.10; // capture anything below this level
	if (PathRoughness >= MaxRoughness)
	{
		// no point tracking past a certain roughness
		return;
	}
	if (Payload.ShadingModelID == SHADINGMODELID_UNLIT)
	{
		return;
	}
	// figure out how much the current path should contribute to the denoiser aovs
	PathThroughput *= 1 - saturate((PathRoughness - MinRoughness) / (MaxRoughness - MinRoughness));
#if PATHTRACING_STRATA_PAYLOAD
	PathThroughput *= Payload.WeightV * Payload.BSDFOpacity * lerp(1.0, Payload.TransmittanceN, Payload.CoverageAboveAlongN);
#endif
	float NormThroughput = Luminance(PathThroughput);
	// figure out how much the specular lobes should contribute to the total (relative to the current PathRoughness)
	// if we are above the MinRoughness, use the PathRoughness as are lower bound instead so that multiple bounces don't accumulate too much albedo
#if PATHTRACING_STRATA_PAYLOAD
	switch (Payload.ShadingModelID)
	{
		case SHADINGMODELID_STRATA:
		{
			const float Roughness0 = Payload.RoughnessData.x;
			const float Roughness1 = Payload.RoughnessData.y;
			const float SpecBlend = Payload.RoughnessData.z;
			const float Roughness0Weight = (1 - SpecBlend) * ROUGHNESS_WEIGHT(Roughness0);
			const float Roughness1Weight = (    SpecBlend) * ROUGHNESS_WEIGHT(Roughness1);
			Albedo += PathThroughput * (Payload.DiffuseColor + Payload.SpecularColor * (Roughness0Weight + Roughness1Weight) + Payload.FuzzAmount * Payload.FuzzColor);
			Normal += NormThroughput * Payload.WorldNormal;
			break;
		}
		case SHADINGMODELID_EYE:
		{
			const float RoughnessWeight = ROUGHNESS_WEIGHT(Payload.GetEyeRoughness());
			Albedo += PathThroughput * (Payload.DiffuseColor + RoughnessWeight * Payload.SpecularColor + Payload.SubsurfaceColor);
			Normal += NormThroughput * Payload.WorldNormal;
			break;
		}
		default:
		{
			Albedo += PathThroughput * Payload.GetBaseColor();
			Normal += NormThroughput * Payload.WorldNormal;
			break;
		}
	}
#else
	const float RoughnessWeight = ROUGHNESS_WEIGHT(Payload.Roughness);
	const float BrdfWeight = Payload.BSDFOpacity;

	switch (Payload.ShadingModelID)
	{
#if SIMPLIFIED_MATERIAL_SHADER
	case SHADINGMODELID_TWOSIDED_FOLIAGE:
		Albedo += BrdfWeight * PathThroughput * (Payload.BaseColor + Payload.SubsurfaceColor);
		Normal += BrdfWeight * NormThroughput * Payload.WorldNormal;
		break;
#else
	case SHADINGMODELID_TWOSIDED_FOLIAGE:
	case SHADINGMODELID_SUBSURFACE:
	case SHADINGMODELID_PREINTEGRATED_SKIN:
	case SHADINGMODELID_SUBSURFACE_PROFILE:
	case SHADINGMODELID_EYE:
		Albedo += BrdfWeight * PathThroughput * (Payload.DiffuseColor + RoughnessWeight * Payload.SpecularColor + Payload.SubsurfaceColor);
		Normal += BrdfWeight * NormThroughput * Payload.WorldNormal;
		break;
	case SHADINGMODELID_DEFAULT_LIT:
	{
		if (Payload.HasRefraction())
		{
			const float GlassAlbedo = 1.0; // solid glass has unit albedo
			Albedo += (1 - BrdfWeight) * RoughnessWeight * PathThroughput * GlassAlbedo;
			Normal += (1 - BrdfWeight) * RoughnessWeight * NormThroughput * Payload.WorldNormal;
		}
		// fallthrough
	}
	case SHADINGMODELID_CLEAR_COAT:
		Albedo += BrdfWeight * PathThroughput * (Payload.DiffuseColor + RoughnessWeight * Payload.SpecularColor);
		Normal += BrdfWeight * NormThroughput * Payload.WorldNormal;
		break;
	case SHADINGMODELID_CLOTH:
		Albedo += BrdfWeight * PathThroughput * (Payload.DiffuseColor + lerp(RoughnessWeight * Payload.SpecularColor, Payload.GetClothColor(), Payload.GetClothAmount()));
		Normal += BrdfWeight * NormThroughput * Payload.WorldNormal;
		break;
	case SHADINGMODELID_THIN_TRANSLUCENT:
	{
		const float F0 = F0RGBToF0(Payload.SpecularColor);
		const float3 GlassAlbedo = Payload.HasRefraction() ? lerp(Payload.GetTransmittanceColor(), 1.0, F0) : F0;
		Albedo += (1 - BrdfWeight) * RoughnessWeight * PathThroughput * GlassAlbedo;
		Normal += (1 - BrdfWeight) * RoughnessWeight * NormThroughput * Payload.WorldNormal;
		Albedo += BrdfWeight * PathThroughput * (Payload.DiffuseColor + RoughnessWeight * Payload.SpecularColor);
		Normal += BrdfWeight * NormThroughput * Payload.WorldNormal;
		break;
	}
#endif // SIMPLIFIED_MATERIAL_SHADER
	default:
		Albedo += BrdfWeight * PathThroughput * Payload.BaseColor;
		Normal += BrdfWeight * NormThroughput * Payload.WorldNormal;
		break;
	}
#endif // PATHTRACING_STRATA_PAYLOAD
}

#undef ROUGHNESS_WEIGHT

void AccumulateAlbedo(float3 SigmaS, float3 PathThroughput, float PathRoughness, inout float3 Albedo)
{
	const float MaxRoughness = 0.15; // don't bother past this level
	const float MinRoughness = 0.10; // capture anything below this level
	if (PathRoughness >= MaxRoughness)
	{
		// no point tracking past a certain roughness
		return;
	}
	// figure out how much the current path should contribute to the denoiser aovs
	PathThroughput *= 1 - saturate((PathRoughness - MinRoughness) / (MaxRoughness - MinRoughness));
	// Volume acts as diffuse scattering, so make it contribute fully (like DiffuseColor above)
	Albedo += SigmaS * PathThroughput;
}

// returns an average roughness for the whole material (for the path roughness of shadow rays from this material)
float GetAverageRoughness(FPathTracingPayload Payload)
{
#if SIMPLIFIED_MATERIAL_SHADER
	return 1.0;
#elif PATHTRACING_STRATA_PAYLOAD
	float AvgRoughness = Payload.RoughnessData.x;
	switch (Payload.ShadingModelID)
	{
		case SHADINGMODELID_STRATA:
		{
			// STRATA_TODO: revisit this when we support glass
			AvgRoughness = max(AvgRoughness, Payload.RoughnessData.y);
			float DiffuseFraction = LobeSelectionProb(Payload.SubsurfaceColor + Payload.DiffuseColor + Payload.FuzzAmount * Payload.FuzzColor, Payload.SpecularColor);
			AvgRoughness = lerp(1.0, AvgRoughness, DiffuseFraction);
			break;
		}
		// STRATA_TODO: do we need special cases for the other slab types?
	}
	return AvgRoughness;
#else
	const float GlassPercent = Payload.HasRefraction() ? 1 - Payload.BSDFOpacity : 0.0;
	float Roughness = Payload.Roughness;
	switch (Payload.ShadingModelID)
	{
		case SHADINGMODELID_CLEAR_COAT:
			Roughness = max(Roughness, Payload.GetClearCoatRoughness());
			break;
		case SHADINGMODELID_SUBSURFACE_PROFILE:
		{
			float3 DualRoughnessData = Payload.GetDualRoughnessSpecular();
			Roughness = max(DualRoughnessData.x, DualRoughnessData.y);
			break;
		}
	}
	float AvgRoughness = lerp(1.0 /* diffuse lobe */, Roughness, Payload.Metallic);
	AvgRoughness = lerp(AvgRoughness, Payload.Roughness, GlassPercent);
	return AvgRoughness;
#endif
}

FPathTracingPayload UnpackPathTracingPayloadMaterial(FPackedPathTracingPayload PackedInput, FRayDesc Ray, bool bApplyOverrides)
{
	// This function can also do a bit of per-material post-processing to simplify the rest of the code
	FPathTracingPayload HitPayload = UnpackPathTracingPayload(PackedInput, Ray);
	if (bApplyOverrides)
	{
		HitPayload.DiffuseColor = HitPayload.DiffuseColor * View.DiffuseOverrideParameter.w + View.DiffuseOverrideParameter.xyz;
		HitPayload.SpecularColor = HitPayload.SpecularColor * View.SpecularOverrideParameter.w + View.SpecularOverrideParameter.xyz;
	}
#if PATHTRACING_STRATA_PAYLOAD
	switch (HitPayload.ShadingModelID)
	{
		case SHADINGMODELID_STRATA:
		{
			// NOTE: if radius is 0 the sss color will get flipped back into diffuse
			HitPayload.SubsurfaceColor = HitPayload.DiffuseColor;
			HitPayload.DiffuseColor = 0;
			break;
		}
		case SHADINGMODELID_EYE:
		{
			// eye still needs a diffuse component for the iris center
			float IrisMask = HitPayload.GetEyeIrisMask();
			HitPayload.SubsurfaceColor = HitPayload.DiffuseColor - IrisMask * HitPayload.DiffuseColor;
			HitPayload.DiffuseColor *= IrisMask;
			break;
		}
	}
#else
	switch (HitPayload.ShadingModelID)
	{
#if !SIMPLIFIED_MATERIAL_SHADER
		case SHADINGMODELID_SUBSURFACE:
		case SHADINGMODELID_PREINTEGRATED_SKIN:
		{
			HitPayload.SubsurfaceRadius = HitPayload.GetSubsurfaceRadius();
			// fallthrough to case below to avoid over-bright SSS
		}
#endif
		case SHADINGMODELID_TWOSIDED_FOLIAGE:
		{
			HitPayload.SubsurfaceColor = HitPayload.GetSubsurfaceColor();
			float3 Front = HitPayload.DiffuseColor;
			float3 Back = HitPayload.SubsurfaceColor;
			float3 Sum = Front + Back;
			float S = max(Sum.x, max(Sum.y, Sum.z));
			if (S > 1)
			{
				// nudge the material back to something physically plausible
				// NOTE: we ignore spec here since it should be accounted for by the brdf model itself
				HitPayload.DiffuseColor = Front / S;
				HitPayload.SubsurfaceColor = Back / S;
			}
			if (bApplyOverrides)
			{
				HitPayload.SubsurfaceColor = HitPayload.SubsurfaceColor * View.DiffuseOverrideParameter.w + View.DiffuseOverrideParameter.xyz;
			}
			break;
		}
#if SIMPLIFIED_MATERIAL_SHADER
		default:
		{
			HitPayload.SubsurfaceColor = 0.0;
			break;
		}
#else
		case SHADINGMODELID_SUBSURFACE_PROFILE:
		{
			HitPayload.SubsurfaceRadius = HitPayload.GetSubsurfaceRadius();
			// NOTE: if radius is 0 the sss color will get flipped back into diffuse
			HitPayload.SubsurfaceColor = HitPayload.DiffuseColor;
			HitPayload.DiffuseColor = 0;
			break;
		}
		case SHADINGMODELID_EYE:
		{
			HitPayload.SubsurfaceRadius = HitPayload.GetSubsurfaceRadius();
			// eye still needs a diffuse component for the iris center
			float IrisMask = HitPayload.GetEyeIrisMask();
			HitPayload.SubsurfaceColor = HitPayload.DiffuseColor - IrisMask * HitPayload.DiffuseColor;
			HitPayload.DiffuseColor *= IrisMask;
			break;
		}
		case SHADINGMODELID_HAIR:
		{
			if (bApplyOverrides)
			{
				HitPayload.BaseColor = HitPayload.BaseColor  * View.SpecularOverrideParameter.w  + View.SpecularOverrideParameter.xyz;
				HitPayload.Specular = HitPayload.Specular * View.SpecularOverrideParameter.w + View.SpecularOverrideParameter.x;
			}
			break;
		}
		case SHADINGMODELID_CLEAR_COAT:
		{
			if (bApplyOverrides)
			{
				HitPayload.SetClearCoat(HitPayload.GetClearCoat() * View.SpecularOverrideParameter.w + View.SpecularOverrideParameter.x);
			}
			break;
		}
		case SHADINGMODELID_CLOTH:
		{
			if (bApplyOverrides)
			{
				HitPayload.SetClothColor(HitPayload.GetClothColor() * View.SpecularOverrideParameter.w + View.SpecularOverrideParameter.xyz);
			}
			break;
		}
#endif
	}
#endif // PATHTRACING_STRATA_PAYLOAD
	return HitPayload;
}

struct FSSSRandomWalkInfo
{
	float3 Color;
	float3 Radius;
	float3 Weight;  // multiplier for the path throughput to take into account energy conservation from specular above the SSS
	float  Prob;    // probability of choosing to perform the random walk
	float  G;
};


FSSSRandomWalkInfo GetMaterialSSSInfo(FPathTracingPayload Payload, float3 V_World)
{
	FSSSRandomWalkInfo Result = (FSSSRandomWalkInfo)0;
#if PATHTRACING_STRATA_PAYLOAD
	if (Payload.ShadingModelID == SHADINGMODELID_STRATA)
	{
		if (any(Payload.MeanFreePath > 0.0))
		{
			Result.Color = Payload.SubsurfaceColor;
			Result.Radius = Payload.MeanFreePath;

			FSubstrateSingleSlabData Data = (FSubstrateSingleSlabData)0;
			Data.PrepareBSDF(V_World, Payload);
			const float3 SSSAlbedo = Payload.SubsurfaceColor * Data.DiffuseWeight;
			const float3 DiffuseAlbedo = Payload.DiffuseColor * Data.DiffuseWeight;
			const float3 Spec0Albedo = Data.FuzzAttenuation * (1.0 - Payload.RoughnessData.z) * Data.Spec0.E;
			const float3 Spec1Albedo = Data.FuzzAttenuation * (Payload.RoughnessData.z) * Data.Spec1.E;
			const float  FuzzAlbedo = Payload.FuzzAmount * Data.Fuzz.E;
			const float3 MaxLobeWeight = Payload.GetMaxLobeWeight();

			Result.Weight = Payload.WeightV * Payload.BSDFOpacity * Data.DiffuseWeight;
			Result.Prob = LobeSelectionProb(
				MaxLobeWeight * (Payload.SubsurfaceColor * Data.DiffuseWeight),
				MaxLobeWeight * (Payload.DiffuseColor * Data.DiffuseWeight + Spec0Albedo + Spec1Albedo + FuzzAlbedo));
			// STRATA_TODO: evaluate if we want to set Result.G in all cases?
		}
	}
	if (Payload.ShadingModelID == SHADINGMODELID_EYE)
	{
		if (any(Payload.MeanFreePath > 0.0))
		{
			Result.Color = Payload.SubsurfaceColor;
			Result.Radius = Payload.MeanFreePath;
			const float NoV = saturate(dot(Payload.WorldNormal, V_World));
			const float3 SpecE = ComputeGGXSpecEnergyTermsRGB(Payload.GetEyeRoughness(), NoV, Payload.SpecularColor).E;

			const float DiffWeight = (1 - Luminance(SpecE));
			const float3 SSSAlbedo  = DiffWeight * Payload.SubsurfaceColor;
			const float3 DiffAlbedo = DiffWeight * Payload.DiffuseColor;
			const float3 SpecAlbedo = SpecE;
			Result.Prob = LobeSelectionProb(SSSAlbedo, DiffAlbedo + SpecAlbedo);
			Result.Weight = Payload.BSDFOpacity * DiffWeight;

		}
	}
#else
	Result.Color = Payload.SubsurfaceColor;
	Result.Radius = Payload.SubsurfaceRadius;
	Result.Weight = 0;
	Result.Prob = 0;
	Result.G = Payload.GetSubsurfacePhaseFunction();
	if (Payload.IsSubsurfaceMaterial())
	{
		const float NoV = saturate(dot(Payload.WorldNormal, V_World));
		float3 SpecE = 0;
		if (Payload.ShadingModelID == SHADINGMODELID_SUBSURFACE_PROFILE)
		{
			// dual spec
			const float3 DualRoughnessData = Payload.GetDualRoughnessSpecular();
			const float3 SpecE0 = ComputeGGXSpecEnergyTermsRGB(DualRoughnessData.x, NoV, Payload.SpecularColor).E;
			const float3 SpecE1 = ComputeGGXSpecEnergyTermsRGB(DualRoughnessData.y, NoV, Payload.SpecularColor).E;
			SpecE = lerp(SpecE0, SpecE1, DualRoughnessData.z);
		}
		else
		{
			// single spec
			SpecE = ComputeGGXSpecEnergyTermsRGB(Payload.Roughness, NoV, Payload.SpecularColor).E;
		}

		const float3 SSSLobeAlbedo = (1 - SpecE) * Payload.SubsurfaceColor;
		const float3 DiffLobeAlbedo = (1 - SpecE) * Payload.DiffuseColor;
		const float3 SpecLobeAlbedo = SpecE;
		Result.Prob = LobeSelectionProb(SSSLobeAlbedo, DiffLobeAlbedo + SpecLobeAlbedo);
		Result.Weight = Payload.BSDFOpacity * (1 - SpecE);
	}
#endif // PATHTRACING_STRATA_PAYLOAD
	return Result;
}

void RemoveMaterialSSS(inout FPathTracingPayload Payload)
{
	// Tweak the payload to remove subsurface scattering without chaning the overall color
	// This function is free to assume this material contains SSS
	Payload.DiffuseColor += Payload.SubsurfaceColor;
	Payload.DiffuseColor = saturate(Payload.DiffuseColor);
	Payload.SubsurfaceColor = 0.0;
}


FMaterialSample SampleMaterial(
	float3 V_World,
	FPathTracingPayload Payload,
	float3 RandSample
)
{
#if SIMPLIFIED_MATERIAL_SHADER
	return Simplified_SampleMaterial(Payload, RandSample);
	//return Lambert_SampleMaterial(Payload, RandSample);
#else
	switch (Payload.ShadingModelID)
	{
		case SHADINGMODELID_UNLIT:
			// Nothing to do, material does not scatter light
			return NullMaterialSample();
#if PATHTRACING_STRATA_PAYLOAD
		case SHADINGMODELID_STRATA:
			return Substrate_SampleMaterial(V_World, Payload, RandSample);
#else
		case SHADINGMODELID_DEFAULT_LIT:
		{
			if (Payload.HasRefraction() && Payload.BSDFOpacity < 1)
			{
				return RoughGlass_SampleMaterial(V_World, Payload, RandSample);
			}
			// fallthrough
		}
		case SHADINGMODELID_SUBSURFACE:
		case SHADINGMODELID_PREINTEGRATED_SKIN:
			return DefaultLit_SampleMaterial(V_World, Payload, RandSample);
		case SHADINGMODELID_THIN_TRANSLUCENT:
			return RoughThinGlass_SampleMaterial(V_World, Payload, RandSample);
		case SHADINGMODELID_SUBSURFACE_PROFILE:
			return SubsurfaceProfile_SampleMaterial(V_World, Payload, RandSample);
		case SHADINGMODELID_CLOTH:
			return Cloth_SampleMaterial(V_World, Payload, RandSample);
		case SHADINGMODELID_CLEAR_COAT:
			return ClearCoat_SampleMaterial(V_World, Payload, RandSample);
		case SHADINGMODELID_TWOSIDED_FOLIAGE:
			return TwoSidedFoliage_SampleMaterial(V_World, Payload, RandSample);
#endif // PATHTRACING_STRATA_PAYLOAD
		case SHADINGMODELID_EYE:
			return Eye_SampleMaterial(V_World, Payload, RandSample);
		case SHADINGMODELID_HAIR:
			return Hair_SampleMaterial(V_World, Payload, RandSample);
		case SHADINGMODELID_MEDIUM:
			return Medium_SampleMaterial(V_World, Payload, RandSample);
		default:
			return Lambert_SampleMaterial(Payload, RandSample);
	}
	return NullMaterialSample();
#endif // SIMPLIFIED_MATERIAL_SHADER
}

FMaterialEval EvalMaterial(
	float3 V_World,
	float3 L_World,
	FPathTracingPayload Payload,
	float2 DiffuseSpecularScale
)
{
#if SIMPLIFIED_MATERIAL_SHADER
	return Simplified_EvalMaterial(L_World, Payload);
	//return Lambert_EvalMaterial(L_World, Payload);
#else
	switch (Payload.ShadingModelID)
	{
		case SHADINGMODELID_UNLIT:
			break;
#if PATHTRACING_STRATA_PAYLOAD
		case SHADINGMODELID_STRATA:
			return Substrate_EvalMaterial(V_World, L_World, Payload, DiffuseSpecularScale);
#else
		case SHADINGMODELID_DEFAULT_LIT:
		{
			if (Payload.HasRefraction() && Payload.BSDFOpacity < 1)
			{
				return RoughGlass_EvalMaterial(V_World, L_World, Payload, DiffuseSpecularScale);
			}
			// fallthrough
		}
		case SHADINGMODELID_SUBSURFACE:
		case SHADINGMODELID_PREINTEGRATED_SKIN:
			return DefaultLit_EvalMaterial(V_World, L_World, Payload, DiffuseSpecularScale);
		case SHADINGMODELID_THIN_TRANSLUCENT:
			return RoughThinGlass_EvalMaterial(V_World, L_World, Payload, DiffuseSpecularScale);
		case SHADINGMODELID_SUBSURFACE_PROFILE:
			return SubsurfaceProfile_EvalMaterial(V_World, L_World, Payload, DiffuseSpecularScale);
		case SHADINGMODELID_CLOTH:
			return Cloth_EvalMaterial(V_World, L_World, Payload, DiffuseSpecularScale);
		case SHADINGMODELID_CLEAR_COAT:
			return ClearCoat_EvalMaterial(V_World, L_World, Payload, DiffuseSpecularScale);
		case SHADINGMODELID_TWOSIDED_FOLIAGE:
			return TwoSidedFoliage_EvalMaterial(V_World, L_World, Payload, DiffuseSpecularScale);
#endif // PATHTRACING_STRATA_PAYLOAD
		case SHADINGMODELID_EYE:
			return Eye_EvalMaterial(V_World, L_World, Payload, DiffuseSpecularScale);
		case SHADINGMODELID_HAIR:
			return Hair_EvalMaterial(V_World, L_World, Payload, DiffuseSpecularScale);
		case SHADINGMODELID_MEDIUM:
			return Medium_EvalMaterial(V_World, L_World, Payload, DiffuseSpecularScale);
		default:
			return Lambert_EvalMaterial(L_World, Payload, DiffuseSpecularScale);
	}
	return NullMaterialEval();
#endif // SIMPLIFIED_MATERIAL_SHADER
}
