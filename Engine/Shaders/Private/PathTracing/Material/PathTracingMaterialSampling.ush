// Copyright Epic Games, Inc. All Rights Reserved.

/*=============================================================================================
PathTracingMaterialSampling.usf: Material sampling functions
===============================================================================================*/

#pragma once

#include "PathTracingMaterialCommon.ush"
#include "PathTracingFresnel.ush"
#include "PathTracingLambert.ush"
#include "PathTracingRadianceProbe.ush"
#include "PathTracingGlossy.ush"
#include "PathTracingSpecularReflection.ush"
#include "PathTracingSpecularTransmission.ush"
#include "PathTracingSolidGlass.ush"
#include "PathTracingThinGlass.ush"
#include "PathTracingDefaultLit.ush"
#include "PathTracingClearCoat.ush"
#include "PathTracingTwoSidedFoliage.ush"
#include "PathTracingCloth.ush"

#ifndef FORCE_ALL_MATERIALS_DIFFUSE
	#define FORCE_ALL_MATERIALS_DIFFUSE 0
#endif

bool IsNeeValidMaterial(FMaterialClosestHitPayload Payload)
{
	// Checks done in the same order as other sampling and evaluation functions to ensure consistency
	
#if FORCE_ALL_MATERIALS_DIFFUSE
	return true;
#else

	if (Payload.BlendingMode == RAY_TRACING_BLEND_MODE_MODULATE ||
		Payload.BlendingMode == RAY_TRACING_BLEND_MODE_ALPHA_HOLDOUT)
	{
		return false;
	}

	switch (Payload.ShadingModelID)
	{
		case SHADINGMODELID_UNLIT:
			return false;
		case SHADINGMODELID_DEFAULT_LIT:		
		case SHADINGMODELID_CLEAR_COAT:			
		case SHADINGMODELID_TWOSIDED_FOLIAGE:	
		case SHADINGMODELID_THIN_TRANSLUCENT:
		case SHADINGMODELID_CLOTH:
		case SHADINGMODELID_SUBSURFACE:
		case SHADINGMODELID_PREINTEGRATED_SKIN:
		default:
			return true;
	}
#endif
}

float3 GetMaterialTransparency(FMaterialClosestHitPayload Payload, float3 RayDirection)
{
	switch (Payload.ShadingModelID)
	{
		case SHADINGMODELID_THIN_TRANSLUCENT:
		{
			if (Payload.Ior == 1.0)
			{
				float VoN = dot(RayDirection, Payload.WorldNormal);
				return (1 - Payload.Opacity) * pow(Payload.CustomData.xyz, 1.0 / abs(VoN));
			}
			else
			{
				return 0;
			}
		}
		case SHADINGMODELID_SUBSURFACE:
		case SHADINGMODELID_PREINTEGRATED_SKIN:
			return 0;
		default:
			// other shading models support blend modes
			break;
	}
	switch (Payload.BlendingMode)
	{
		case RAY_TRACING_BLEND_MODE_TRANSLUCENT:
			if (Payload.Ior == 1.0)
			{
				// material is not configured with refraction, can use transparency
				return 1 - Payload.Opacity;
			}
			else
			{
				// not using transparency - material will be opaque
				return 0;
			}
		case RAY_TRACING_BLEND_MODE_ADDITIVE:
			return 1;
		case RAY_TRACING_BLEND_MODE_MODULATE:
			return Payload.Radiance;
		case RAY_TRACING_BLEND_MODE_ALPHA_COMPOSITE:
			return 1 - Payload.Opacity;
		case RAY_TRACING_BLEND_MODE_ALPHA_HOLDOUT:
			return 1;
		default: 
			return 0;
	}
}

float3 EstimateMaterialAlbedo(FMaterialClosestHitPayload Payload)
{
	float BrdfWeight = 1;
	bool HasRefraction = false;
	switch (Payload.BlendingMode)
	{
		case RAY_TRACING_BLEND_MODE_TRANSLUCENT:
			if (Payload.Ior == 1.0)
			{
				// material is not configured with refraction, can use transparency
				// NOTE: the albedo when considering refraction is handled below
				BrdfWeight = Payload.Opacity;
			}
			else
			{
				HasRefraction = true;
			}
			break;
		case RAY_TRACING_BLEND_MODE_ADDITIVE:
			BrdfWeight = Payload.Opacity;
			break;
		case RAY_TRACING_BLEND_MODE_MODULATE:
		case RAY_TRACING_BLEND_MODE_ALPHA_HOLDOUT:
			// In these modes, the light does not interact with the material at all, only passes through
			return 0;
		default:
			break;
	}

	float3 Albedo = 0;
	switch (Payload.ShadingModelID)
	{
		case SHADINGMODELID_UNLIT:
			// this object does not scatter light
			return 0;
		case SHADINGMODELID_TWOSIDED_FOLIAGE:
			Albedo = BrdfWeight * (Payload.DiffuseColor + Payload.SpecularColor + GetSubsurfaceColor(Payload));
			break;
		case SHADINGMODELID_DEFAULT_LIT:
		case SHADINGMODELID_CLEAR_COAT:
			// FIXME: could make this more accurate by estimating the directional albedo
			Albedo = BrdfWeight * (Payload.DiffuseColor + Payload.SpecularColor);
		case SHADINGMODELID_CLOTH:
			Albedo = BrdfWeight * (Payload.DiffuseColor + lerp(Payload.SpecularColor, Payload.CustomData.xyz, Payload.CustomData.w));
			break;
		case SHADINGMODELID_SUBSURFACE:
		case SHADINGMODELID_PREINTEGRATED_SKIN:
			Albedo = BrdfWeight * (Payload.DiffuseColor + Payload.SpecularColor + Payload.CustomData.xyz);
			break;
		default:
			Albedo = BrdfWeight * Payload.BaseColor;
			break;
	}
	if (HasRefraction)
	{
		// glass is currently perfectly clear - so has unit albedo
		// TODO: estimate some kind of albedo for thin-translucent case since that one uses a color?
		float3 White = 1;
		Albedo = lerp(White, Albedo, Payload.Opacity);
	}
	return Albedo;
}

void AdjustPathAndMaterialRoughness(inout FMaterialClosestHitPayload Payload, inout float PathRoughness)
{
	// boost material roughness according to what we've seen so far
	float CoatRoughness = Payload.Roughness;
	switch (Payload.ShadingModelID)
	{
		case SHADINGMODELID_UNLIT:
			// this object does not scatter light
			return;
		case SHADINGMODELID_CLEAR_COAT:
			// clamp coat roughness
			CoatRoughness = Payload.CustomData.y = max(GetClearCoatRoughness(Payload), PathRoughness);
			// fallthrough
		case SHADINGMODELID_TWOSIDED_FOLIAGE:
		case SHADINGMODELID_DEFAULT_LIT:
		case SHADINGMODELID_THIN_TRANSLUCENT:
		case SHADINGMODELID_CLOTH:
		case SHADINGMODELID_SUBSURFACE:
		case SHADINGMODELID_PREINTEGRATED_SKIN:
			Payload.Roughness = max(Payload.Roughness, PathRoughness);
			break;
		default:
			// fallback is diffuse
			Payload.Roughness = 1.0;
			break;
	}
	// now set the path roughness going forward
	const float GlassPercent = (Payload.BlendingMode == RAY_TRACING_BLEND_MODE_TRANSLUCENT && Payload.Ior != 1.0) ? 1 - Payload.Opacity : 0.0;
	PathRoughness = lerp(1.0 /* diffuse lobe */, max(CoatRoughness, Payload.Roughness), Payload.Metallic);
	PathRoughness = lerp(PathRoughness, Payload.Roughness, GlassPercent);
}

FMaterialSample SampleMaterial(
	float3 RayDirection,
	FMaterialClosestHitPayload Payload,
	float4 RandSample,
	bool bForceRadianceProbe
)
{
	if (bForceRadianceProbe)
	{
		return RadianceProbe_SampleMaterial(Payload, RandSample);
	}
	
#if FORCE_ALL_MATERIALS_DIFFUSE
	{
		return Lambert_SampleMaterial(Payload, RandSample);
	}
#else

	if (Payload.BlendingMode == RAY_TRACING_BLEND_MODE_MODULATE ||
		Payload.BlendingMode == RAY_TRACING_BLEND_MODE_ALPHA_HOLDOUT)
	{
		// transparency only, nothing to sample
		return NullMaterialSample();
	}

	// Decide the overall weight of the BRDF portion vs. the translucent portion
	// Note that the translucent portion could be refraction, transparency or SSS
	float BrdfWeight = 1;
	float TransWeight = 0;
	float BrdfProb = 1;
	switch (Payload.BlendingMode)
	{
		case RAY_TRACING_BLEND_MODE_TRANSLUCENT:
			if (Payload.Ior == 1.0)
			{
				// scale down BRDF contribution, but don't pick any indirect ray because its already accounted for by transparency handling
				BrdfWeight = Payload.Opacity;
			}
			else
			{
				// randomly choose between brdf and glass
				BrdfWeight = 1;
				TransWeight = 1;
				BrdfProb = Payload.Opacity;
			}
			break;
		case RAY_TRACING_BLEND_MODE_ADDITIVE:
			BrdfWeight = Payload.Opacity;
			break;
		default:
			// opaque or one of the modes handled above
			break;
	}

	FMaterialSample Result = NullMaterialSample();
	if (RandSample.x < BrdfProb)
	{
		// we randomly decided to sample the brdf portion of the material
		RandSample.x /= BrdfProb;
	}
	else
	{
		// we randomly decided to sample the translucent portion
		RandSample.x = (RandSample.x - BrdfProb) / (1 - BrdfProb);

		// only case that should reach this point is the refraction model (which can be thin or solid)
		if (Payload.ShadingModelID == SHADINGMODELID_THIN_TRANSLUCENT)
		{
			Result = RoughThinGlass_SampleMaterial(RayDirection, Payload, RandSample);
		}
		else if (Payload.Roughness == 0)
		{
			Result = SpecularRefraction_SampleMaterial(RayDirection, Payload, RandSample);
		}
		else
		{
			Result = RoughGlass_SampleMaterial(RayDirection, Payload, RandSample);
		}
		Result.Weight *= TransWeight;
		Result.Pdf *= 1 - BrdfProb;
		return Result;
	}

	switch (Payload.ShadingModelID)
	{
		case SHADINGMODELID_UNLIT:
			// Nothing to do, material does not scatter light
			return Result;
		case SHADINGMODELID_DEFAULT_LIT:
		case SHADINGMODELID_SUBSURFACE:
		case SHADINGMODELID_PREINTEGRATED_SKIN:
			Result = DefaultLit_SampleMaterial(RayDirection, Payload, RandSample);
			break;
		case SHADINGMODELID_CLOTH:
			Result = Cloth_SampleMaterial(RayDirection, Payload, RandSample);
			break;
		case SHADINGMODELID_CLEAR_COAT:
			Result = ClearCoat_SampleMaterial(RayDirection, Payload, RandSample);
			break;
		case SHADINGMODELID_TWOSIDED_FOLIAGE:
			Result = TwoSidedFoliage_SampleMaterial(RayDirection, Payload, RandSample);
			break;
		default:
			Result = Lambert_SampleMaterial(Payload, RandSample);
			break;
	}

	// final adjustment to weight / prob based on the stochastic decision for translucency earlier
	Result.Weight *= BrdfWeight;
	Result.Pdf *= BrdfProb;
	return Result;
#endif
}

FMaterialEval EvalMaterial(
	float3 IncomingDirection,
	float3 OutgoingDirection,
	FMaterialClosestHitPayload Payload,
	bool bForceRadianceProbe
)
{
	if (bForceRadianceProbe)
	{
		return RadianceProbe_EvalMaterial(OutgoingDirection, Payload);
	}
	
#if FORCE_ALL_MATERIALS_DIFFUSE
	{
		return Lambert_EvalMaterial(OutgoingDirection, Payload);
	}
#else

	if (Payload.BlendingMode == RAY_TRACING_BLEND_MODE_MODULATE ||
		Payload.BlendingMode == RAY_TRACING_BLEND_MODE_ALPHA_HOLDOUT)
	{
		return NullMaterialEval();
	}

	// Decide the overall weight of the BRDF portion vs. the translucent portion
	// Note that the translucent portion could be refraction, transparency or SSS
	float BrdfWeight = 1;
	float TransWeight = 0;
	float BrdfProb = 1;
	switch (Payload.BlendingMode)
	{
		case RAY_TRACING_BLEND_MODE_TRANSLUCENT:
			// brdf:   Opacity
			// glass:  1.0 - Opacity
			if (Payload.Ior == 1.0)
			{
				// handled by native transpaarency, just weight down the brdf contrib
				BrdfWeight = Payload.Opacity;
			}
			else
			{
				// brdf or glass
				BrdfWeight = 1;
				TransWeight = 1;
				BrdfProb = Payload.Opacity;
			}
			break;
		case RAY_TRACING_BLEND_MODE_ADDITIVE:
			// brdf:   Opacity
			BrdfWeight = Payload.Opacity;
			break;
		default:
			// opaque or one of the modes handled above
			break;
	}

	// NOTE: the translucent portions are all singular -- so we can ignore them in the eval method for now as they have Pdf=+inf for a single direction only

	FMaterialEval Result = NullMaterialEval();
	switch (Payload.ShadingModelID)
	{
		case SHADINGMODELID_UNLIT:
			break;
		case SHADINGMODELID_DEFAULT_LIT:
		case SHADINGMODELID_SUBSURFACE:
		case SHADINGMODELID_PREINTEGRATED_SKIN:
			Result = DefaultLit_EvalMaterial(IncomingDirection, OutgoingDirection, Payload);
			break;
		case SHADINGMODELID_CLOTH:
			Result = Cloth_EvalMaterial(IncomingDirection, OutgoingDirection, Payload);
			break;
		case SHADINGMODELID_CLEAR_COAT:
			Result = ClearCoat_EvalMaterial(IncomingDirection, OutgoingDirection, Payload);
			break;
		case SHADINGMODELID_TWOSIDED_FOLIAGE:
			Result = TwoSidedFoliage_EvalMaterial(IncomingDirection, OutgoingDirection, Payload);
			break;
		default:
			Result = Lambert_EvalMaterial(OutgoingDirection, Payload);
			break;
	}

	// final adjustment to weight / prob for translucency
	Result.Weight *= BrdfWeight;
	Result.Pdf *= BrdfProb;

	if (BrdfProb < 1.0)
	{
		// refraction model (only evaluate for rough case as singular can't be evaluated)
		if (Payload.Roughness != 0)
		{
			FMaterialEval GlassEval;
			if (Payload.ShadingModelID == SHADINGMODELID_THIN_TRANSLUCENT)
			{
				// just a thin slab (reflection-like lobe on both sides)
				GlassEval = RoughThinGlass_EvalMaterial(IncomingDirection, OutgoingDirection, Payload);
			}
			else
			{
				// real solid glass, reflection and refraction
				GlassEval = RoughGlass_EvalMaterial(IncomingDirection, OutgoingDirection, Payload);
			}
			

			AddLobeWithMIS(Result.Weight, Result.Pdf, GlassEval.Weight, GlassEval.Pdf, 1.0 - BrdfProb);
		}
	}
	return Result;
#endif
}
