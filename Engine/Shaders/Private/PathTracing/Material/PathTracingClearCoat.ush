// Copyright 1998-2019 Epic Games, Inc. All Rights Reserved.

/*=============================================================================================
	PathTracingClearCoat.usf: Path tracing BRDF model for clear coat material
===============================================================================================*/

#pragma once

#define EVAL_TOP_LAYER 1
#define EVAL_BOTTOM_LAYER 1
#define EVAL_REFRACTION 1

void CalcFresnelCoefficients(float CosTheta1, float N1, float N2, out float CosTheta2, out float Rs, out float Rp)
{
	Rs = 0.0;
	Rp = 0.0;
	CosTheta2 = 0.0;
	// Use Snell's law to compute CosTheta2:
	// N1 * sin(Theta1) = N2 * sin(Theta2)
	// [N1 * sin(Theta1)]^2 = [N2 * sin(Theta2)]^2
	float SinTheta1_Squared = 1.0 - CosTheta1 * CosTheta1;
	float SinTheta2_Squared = (SinTheta1_Squared * N1 * N1) / (N2 * N2);

	float CosTheta1_Squared = 1.0 - SinTheta1_Squared;
	float CosTheta2_Squared = 1.0 - SinTheta2_Squared;
	if (CosTheta2_Squared >= 0.0)
	{
		CosTheta2 = sqrt(CosTheta2_Squared);

		// Optimized Fresnel equations
		// Rs = (N1 * CosTheta1 - N2 * CosTheta2) / (N1 * CosTheta1 + N2 * CosTheta2);
		// Rp = (N2 * CosTheta1 - N1 * CosTheta2) / (N2 * CosTheta1 + N1 * CosTheta2);
		float N1_CosTheta1 = N1 * CosTheta1;
		float N1_CosTheta2 = N1 * CosTheta2;
		float N2_CosTheta1 = N2 * CosTheta1;
		float N2_CosTheta2 = N2 * CosTheta2;
		Rs = (N1_CosTheta1 - N2_CosTheta2) / (N1_CosTheta1 + N2_CosTheta2);
		Rp = (N2_CosTheta1 - N1_CosTheta2) / (N2_CosTheta1 + N1_CosTheta2);
	}
}

float FresnelReflectance(float CosTheta1, float N1, float N2)
{
	float CosTheta2;
	float Rs;
	float Rp;
	CalcFresnelCoefficients(CosTheta1, N1, N2, CosTheta2, Rs, Rp);

	float Fr = (Rs * Rs + Rp * Rp) / 2.0;
	return Fr;
}

float FresnelTransmittance(float CosTheta1, float N1, float N2)
{
	float Ft = 0.0;
	if (CosTheta1 > 0.0)
	{
		float CosTheta2;
		float Rs;
		float Rp;
		CalcFresnelCoefficients(CosTheta1, N1, N2, CosTheta2, Rs, Rp);

		float Ts = Rs + 1;
		float Tp = (Rp + 1) * N1 / N2;
		Ft = (Ts * Ts + Tp * Tp) / 2.0;
		Ft *= (N2 * CosTheta2) / (N1 * CosTheta1);
	}

	return Ft;
}

float3 CalcThinTransmission(float NoL, float NoV, FMaterialClosestHitPayload Payload)
{
	float3 Transmission = 0.0;
	if (NoL > 0.0 && NoV > 0.0)
	{
		float LayerThickness = GetClearCoatThickness(Payload);
		float ThinDistance = LayerThickness * (rcp(NoV) + rcp(NoL));
		float3 TransmissionColor = GetBaseColor(Payload);
		float OpticalDepth = TransmissionColor * ThinDistance;
		Transmission = exp(-OpticalDepth);
	}
	return Transmission;
}

float CalcClearCoatFresnel(float VoH, FMaterialClosestHitPayload HitInfo)
{
	float Ior1 = 1.0;
	float Ior2 = GetClearCoatIor(HitInfo);
	return FresnelReflectance(VoH, Ior1, Ior2);
}

float CalcClearCoatSelectionPdf(float3 V, float3 H, FMaterialClosestHitPayload Payload)
{
	// Lobe selection is determined by Fresnel term
	float VoH = saturate(dot(V, H));
	float Pdf = CalcClearCoatFresnel(VoH, Payload);

#if EVAL_TOP_LAYER == 0
	Pdf = 0.0;
#elif EVAL_BOTTOM_LAYER == 0
	Pdf = 1.0;
#endif
	return Pdf;
}

float ClearCoat_PdfMaterial(
	float3 IncomingDirection,
	float3 OutgoingDirection,
	FMaterialClosestHitPayload Payload
)
{
	// Compute GGX PDF
	float3 V = -IncomingDirection;
	float3 L = OutgoingDirection;
	float3 H = normalize(V + L);
	float3 N = GetWorldNormal(Payload);

	float NoH = saturate(dot(N, H));
	float HoV = saturate(dot(H, V));

	// Compute top-layer pdf
	float a2 = Pow4(GetClearCoatRoughness(Payload));
	float D = D_GGX(a2, NoH);
	float MicrofacetPdf = 0.0;
	if (HoV > 0.0)
	{
		MicrofacetPdf = D * NoH / (4.0 * HoV);
	}

	// Refract V and L
	float Eta = 1.0 / GetClearCoatIor(Payload);
	#if EVAL_REFRACTION == 1
	float3 V2;
	float3 L2;
	bool IsValidV2 = BSDF_Refract(V, H, Eta, V2);
	bool IsValidL2 = BSDF_Refract(L, H, Eta, L2);
	#else
		float3 V2 = -V;
		float3 L2 = -L;
		bool IsValidV2 = true;
		bool IsValidL2 = true;
	#endif

	// Compute bottom-layer pdf
	float BottomLayerPdf = 0.0;
	if (IsValidV2 && IsValidL2)
	{
		Payload.WorldNormal = GetClearCoatBottomNormal(Payload);
		BottomLayerPdf = DefaultLit_PdfMaterial(V2, -L2, Payload);
		Payload.WorldNormal = N;
	}
	
	// Return piecewise pdf
	float ClearCoatSelectionPdf = CalcClearCoatSelectionPdf(V, H, Payload);
	float Pdf = ClearCoatSelectionPdf * MicrofacetPdf + (1.0 - ClearCoatSelectionPdf) * BottomLayerPdf;
	return Pdf;
}

void ClearCoat_EvalMaterial(
	float3 IncomingDirection,
	float3 OutgoingDirection,
	inout FMaterialClosestHitPayload Payload,
	out float3 OutThroughput,
	out float OutPdf
)
{
	float3 V = -IncomingDirection;
	float3 L = OutgoingDirection;
	float3 H = normalize(V + L);
	float3 N = GetWorldNormal(Payload);

	float NoL = saturate(dot(N, L));
	float NoV = saturate(dot(N, V));
	float NoH = saturate(dot(N, H));
	float VoH = saturate(dot(V, H));

	// top-layer clear coat evaluation
	OutThroughput = 0.0;

#if EVAL_TOP_LAYER == 1
	float CC_a2 = Pow4(GetClearCoatRoughness(Payload));
	float CC_F = CalcClearCoatFresnel(VoH, Payload);
	float CC_D = D_GGX(CC_a2, NoH);
	float CC_G = Vis_SmithJointApprox(CC_a2, NoV, NoL) * (4.0 * NoV * NoL);

	float3 TopLayerThroughput = 0.0;
	if (NoV * NoL > 0.0)
	{
		float ClearCoat = GetClearCoat(Payload);
		// dxr_todo: NoL cancels
		TopLayerThroughput = ClearCoat * ((CC_F * CC_D * CC_G) / (4.0 * NoV * NoL)) * NoL;
	}

	OutThroughput += TopLayerThroughput;
#endif
	
	// bottom-layer clear coat evaluation
#if EVAL_BOTTOM_LAYER == 1
	float Eta = 1.0 / GetClearCoatIor(Payload);
	#if EVAL_REFRACTION == 1
	float3 V2;
	float3 L2;
	bool IsValidV2 = BSDF_Refract(V, H, Eta, V2);
	bool IsValidL2 = BSDF_Refract(L, H, Eta, L2);
	#else
		float3 V2 = -V;
		float3 L2 = -L;
		bool IsValidV2 = true;
		bool IsValidL2 = true;
	#endif
	if (IsValidV2 && IsValidL2)
	{
		V2 = -V2;
		L2 = -L2;
		float3 N2 = GetClearCoatBottomNormal(Payload);
		float N2oL2 = saturate(dot(N2, L2));
		float N2oV2 = saturate(dot(N2, V2));

		// Evaluate bottom coat
		Payload.WorldNormal = N2;
		float3 BottomLayerThroughput;
		float BottomLayerPdf;
		DefaultLit_EvalMaterial(-V2, L2, Payload, BottomLayerThroughput, BottomLayerPdf);
		Payload.WorldNormal = N;

		// Calculate transmission through substrate
		float3 Transmission = CalcThinTransmission(N2oL2, N2oV2, Payload);

		// Apply fresnel through exit interface
		float CC_F1 = CalcClearCoatFresnel(VoH, Payload);
		float HoL = saturate(dot(H, L));
		float CC_F2 = CalcClearCoatFresnel(HoL, Payload);

		OutThroughput += (1.0 - CC_F1) * (1.0 - CC_F2) * Transmission * BottomLayerThroughput;
	}
#endif

	OutPdf = ClearCoat_PdfMaterial(IncomingDirection, OutgoingDirection, Payload);
}

void ClearCoat_SampleMaterial(
	float3 RayDirection,
	FMaterialClosestHitPayload Payload,
	float4 RandSample,
	out float3 OutDirection,
	out float3 OutThroughput,
	out float OutPdf
)
{
	float3 N = GetWorldNormal(Payload);
	float3 V = -RayDirection;
	OutDirection = 0.0;
	OutThroughput = 0.0;
	OutPdf = 0.0;

	// Sample microfacet
	float a2 = Pow4(GetClearCoatRoughness(Payload));
	float4 Result = ImportanceSampleGGX(RandSample.zw, a2);
	float3 H = TangentToWorld(Result.xyz, N);
	float VoH = saturate(dot(V, H));

	float ClearCoatSelectionPdf = CalcClearCoatSelectionPdf(V, H, Payload);
	if (RandSample.x < ClearCoatSelectionPdf)
	{
		float NoH = saturate(dot(N, H));
		float D = D_GGX(a2, NoH);
		float MicrofacetPdf = 0.0;
		if (VoH > 0.0)
		{
			MicrofacetPdf = D * NoH / (4.0 * VoH);

			// Lighting direction is the reflection vector about the sampled microfacet
			float3 R = 2.0 * dot(V, H) * H - V;
			OutDirection = R;
			OutPdf = ClearCoatSelectionPdf * MicrofacetPdf;
			{
				// Calculate throughput
				float3 L = OutDirection;
				float NoL = saturate(dot(N, L));
				float NoV = saturate(dot(N, V));

				// top-layer clear coat evaluation
				float CC_a2 = Pow4(GetClearCoatRoughness(Payload));
				float CC_F = CalcClearCoatFresnel(VoH, Payload);
				float CC_D = D_GGX(CC_a2, NoH);
				float CC_G = Vis_SmithJointApprox(CC_a2, NoV, NoL) * (4.0 * NoV * NoL);

				if (NoV * NoL > 0.0)
				{
					float ClearCoat = GetClearCoat(Payload);
					// dxr_todo: NoL terms cancel
					OutThroughput = ClearCoat * ((CC_F * CC_D * CC_G) / (4.0 * NoV * NoL)) * NoL;
				}
			}
		}
	}
	else
	{
		RandSample.x -= ClearCoatSelectionPdf;
		RandSample.x /= (1.0 - ClearCoatSelectionPdf);

		// Refract V through clear coat
		float Eta = 1.0 / GetClearCoatIor(Payload);
		#if EVAL_REFRACTION == 1
		float3 V2;
		bool IsValidV2 = BSDF_Refract(V, N, Eta, V2);
		V2 = -V2;
		#else
			bool IsValidV2 = true;
			float3 V2 = V;
		#endif

		if (IsValidV2)
		{
			// Sample DefaultLit
			float3 N2 = GetClearCoatBottomNormal(Payload);
			Payload.WorldNormal = N2;

			float3 L2;
			float3 BottomLayerThroughput;
			float BottomLayerPdf;
			DefaultLit_SampleMaterial(-V2, Payload, RandSample, L2, BottomLayerThroughput, BottomLayerPdf);
			Payload.WorldNormal = N;

			// Refract outgoing ray
			#if EVAL_REFRACTION == 1
			bool IsValidL2 = BSDF_Refract(-L2, -N, 1.0 / Eta, OutDirection);
			#else
				OutDirection = L2;
				bool IsValidL2 = true;
			#endif
			if (BottomLayerPdf > 0.0 && IsValidL2)
			{
				// Calculate transmission through substrate
				float N2oL2 = saturate(dot(N2, L2));
				float N2oV2 = saturate(dot(N2, V2));
				float3 Transmission = CalcThinTransmission(N2oL2, N2oV2, Payload);

				// Apply fresnel through exit interface
				float NoV = saturate(dot(N, V));
				float CC_F1 = CalcClearCoatFresnel(NoV, Payload);
				float NoL = saturate(dot(N, OutDirection));
				float CC_F2 = CalcClearCoatFresnel(NoL, Payload);

				OutThroughput = (1.0 - CC_F1) * (1.0 - CC_F2) * Transmission * BottomLayerThroughput;
				OutPdf = (1.0 - ClearCoatSelectionPdf) * BottomLayerPdf;
			}
		}
	}
}
