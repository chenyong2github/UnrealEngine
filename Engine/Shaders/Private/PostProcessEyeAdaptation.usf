// Copyright 1998-2019 Epic Games, Inc. All Rights Reserved.

/*=============================================================================
	PostProcessEyeAdaptation.usf: PostProcessing eye adaptation
=============================================================================*/

#include "Common.ush"
#include "PostProcessCommon.ush"
#include "PostProcessHistogramCommon.ush"

// prior frame's eye adaptation exposure scale
Texture2D EyeAdaptationTexture;
uint4 EyeAdaptionSrcRect;

// Compute the average alpha stored in a 2D texture.
// in uint4 ROI:  region of interest.  (min, min, max, max).
// in float Slope 
float ComputeWeightedTextureAverageAlpha(Texture2D InBuffer, in uint4 ROI, in float Slope)
{
	// The inverse of the Region of Interest size.
	const float InvXMax = 1.f / float(ROI.z - ROI.x);
	const float InvYMax = 1.f / float(ROI.w - ROI.y);

	// use product of linear weight in x and y.  

	float Average = 0.0f;
	float WeightTotal = 0.0f;


	for (uint i = ROI.x; i < ROI.z; ++i)
	{
		float IWeight = AdaptationWeight( i * InvXMax, Slope);  
		for (uint j = ROI.y; j < ROI.w; ++j)
		{
			float JWeight = AdaptationWeight( j * InvYMax, Slope);
			float Weight = max(IWeight * JWeight, 0.05f);
			
			WeightTotal += Weight;

			//accumulate values from alpha channel
			float Sample = InBuffer.Load(int3(i, j, 0)).w;
			Average += Weight * Sample;
		}
	}

	Average /= WeightTotal;
	return Average;
}

float2 ComputeWeightedTextureAverageAlphaSubRegion(Texture2D InBuffer, in uint4 SubROI, in uint4 MainROI, in float Slope)
{
	// The inverse of the Region of Interest size.
	const float InvXMax = 1.f / float(MainROI.z - MainROI.x);
	const float InvYMax = 1.f / float(MainROI.w - MainROI.y);

	// use product of linear weight in x and y.  

	float Value = 0.0f;
	float WeightTotal = 0.0000000001f;	

	for (uint i = SubROI.x; i < SubROI.z; ++i)
	{
		float IWeight = AdaptationWeight( i * InvXMax, Slope);  
		for (uint j = SubROI.y; j < SubROI.w; ++j)
		{
			float JWeight = AdaptationWeight( j * InvYMax, Slope);
			float Weight = max(IWeight * JWeight, 0.05f);
			
			WeightTotal += Weight;

			//accumulate values from alpha channel
			float Sample = InBuffer.Load(int3(i, j, 0)).w;
			Value += Weight * Sample;
		}
	}

	return  float2(Value, WeightTotal);
}


// @param FrameTime in seconds
// @return smoothed exposure
float ComputeEyeAdaptation(float OldExposure, float TargetExposure, float FrameTime)
{
	float Diff = TargetExposure - OldExposure;

	float AdaptionSpeed = (Diff > 0) ? EyeAdaptation_ExposureSpeedUp : EyeAdaptation_ExposureSpeedDown;

	float Factor = 1.0f - exp2(-FrameTime * AdaptionSpeed);

	return clamp(OldExposure + Diff * Factor, EyeAdaptation_MinAverageLuminance, EyeAdaptation_MaxAverageLuminance);
}

Texture2D HistogramTexture;

float4 MainEyeAdaptationCommon()
{
	float4 OutColor = 0;

	const float AverageSceneLuminance = ComputeEyeAdaptationExposure(HistogramTexture);
	const float TargetExposure = clamp(AverageSceneLuminance, EyeAdaptation_MinAverageLuminance, EyeAdaptation_MaxAverageLuminance);

	float OldExposureScale = HistogramTexture.Load(int3(0, 1, 0)).x;
	float OldExposure = EyeAdaptation_ExposureCompensation / ( OldExposureScale != 0 ? OldExposureScale : 1.0f );

	// eye adaptation changes over time
	float SmoothedExposure = ComputeEyeAdaptation(OldExposure, TargetExposure, EyeAdaptation_DeltaWorldTime);

	float SmoothedExposureScale = 1.0f / max(0.0001f, SmoothedExposure);
	float TargetExposureScale =   1.0f / max(0.0001f, TargetExposure);

	OutColor.x = EyeAdaptation_ExposureCompensation * SmoothedExposureScale;
	OutColor.y = EyeAdaptation_ExposureCompensation * TargetExposureScale;
	OutColor.z = AverageSceneLuminance;
	OutColor.w = 0;

	return OutColor;
}

void MainPS(float4 UVAndScreenPos : TEXCOORD0, 
	out float4 OutColor : SV_Target0)
{
	OutColor = MainEyeAdaptationCommon();
}

#if COMPUTESHADER
RWTexture2D<float4> RWEyeAdaptationTexture;

[numthreads(1, 1, 1)]
void MainCS(uint2 DispatchThreadId : SV_DispatchThreadID)
{
	float4 OutColor = MainEyeAdaptationCommon();

	uint2 PixelPos = DispatchThreadId;
	RWEyeAdaptationTexture[PixelPos] = OutColor;
}
#endif

Texture2D SceneColorTexture;

SamplerState SceneColorSampler;

void MainBasicEyeAdaptationSetupPS(
	noperspective float4 UVAndScreenPos : TEXCOORD0,
	out float4 OutColor : SV_Target0)
{
	float2 UV = UVAndScreenPos.xy;
	OutColor = Texture2DSample(SceneColorTexture, SceneColorSampler, UV);

	// Use max to ensure intensity is never zero (so the following log is well behaved)
	float Intensity = max(dot(OutColor.xyz, float3(0.2126, 0.7152, 0.0722)), EyeAdaptation_LuminanceMin);

	// Store log intensity in the alpha channel: scale to 0,1 range.
	OutColor.w = EyeAdaptation_HistogramScale * log2(Intensity) + EyeAdaptation_HistogramBias; 
}


// vertex shader entry point
void MainReductionVS(
	in float4 InPosition : ATTRIBUTE0,
	in float2 UV : ATTRIBUTE1,
	out float2 OutUV : TEXCOORD0,
	out float4 OutPosition : SV_POSITION
	)
{
	DrawRectangle(InPosition, UV, OutPosition, OutUV);
}

// pixel shader entry point
void MainReductionPS(
	noperspective float2 InUV : TEXCOORD0,
	out float4 OutColor : SV_Target0
	)
{
	// Output: low quality, 1 filtered sample
	OutColor = Texture2DSample(PostprocessInput0, PostprocessInput0Sampler, InUV);
}

#if COMPUTESHADER

#define TGSIZE 16
groupshared float2 SubRectValueWeight[TGSIZE*TGSIZE];
RWTexture2D<float4> RWOutputComputeTex;

[numthreads(TGSIZE, TGSIZE, 1)]
void MainLogLuminance2ExposureScaleCS(uint GIndex : SV_GroupIndex, uint2 GTId : SV_GroupThreadID)
{
	float4 OutColor=0;

#else

void MainLogLuminance2ExposureScalePS(in float4 UVAndScreenPos : TEXCOORD0, out float4 OutColor : SV_Target0)
{

#endif

#if USE_PREEXPOSURE
	const float OneOverPreExposure = View.OneOverPreExposure;
#else
	const float OneOverPreExposure = 1.f;
#endif

	// Weighting is a triangle function focused at the center of the screen
	// with the product of linear decay in x and y.  
	// WeightSlope: 0 is uniform weighting.
	//              1 decays to the edges.
	//              >1 focuses more in the center of the screen.
	float WeightSlope = EyeAdaptation_WeightSlope;
	
	// Compute scaled Log Luminance Average
#if COMPUTESHADER
	// There are TGSIZE*TGSIZE threads. Each thread will calculate the luminance for its own subregions from a TGSIZE*TGSIZE screen grid
	uint4 SubRect = uint4(	((EyeAdaptionSrcRect.z-EyeAdaptionSrcRect.x) * GTId.x)		/ TGSIZE,
							((EyeAdaptionSrcRect.w-EyeAdaptionSrcRect.y) * GTId.y)		/ TGSIZE, 
							((EyeAdaptionSrcRect.z-EyeAdaptionSrcRect.x) * (GTId.x+1))	/ TGSIZE  ,
							((EyeAdaptionSrcRect.w-EyeAdaptionSrcRect.y) * (GTId.y+1))	/ TGSIZE  );

	float2 LogLumAveWithWeight = ComputeWeightedTextureAverageAlphaSubRegion(PostprocessInput0, SubRect,  EyeAdaptionSrcRect, WeightSlope);

	// Store in LDS
	SubRectValueWeight[GIndex] = LogLumAveWithWeight;
	GroupMemoryBarrierWithGroupSync();

	// Merge the ValueWeight from all threads
	SubRectValueWeight[GIndex] =  SubRectValueWeight[GIndex] + SubRectValueWeight[GIndex ^ 1];
	GroupMemoryBarrierWithGroupSync();

	SubRectValueWeight[GIndex] =  SubRectValueWeight[GIndex] + SubRectValueWeight[GIndex ^ 2];
	GroupMemoryBarrierWithGroupSync();

	SubRectValueWeight[GIndex] =  SubRectValueWeight[GIndex] + SubRectValueWeight[GIndex ^ 4];
	GroupMemoryBarrierWithGroupSync();

	SubRectValueWeight[GIndex] =  SubRectValueWeight[GIndex] + SubRectValueWeight[GIndex ^ 8];
	GroupMemoryBarrierWithGroupSync();

	SubRectValueWeight[GIndex] =  SubRectValueWeight[GIndex] + SubRectValueWeight[GIndex ^ 16];
	GroupMemoryBarrierWithGroupSync();

	SubRectValueWeight[GIndex] =  SubRectValueWeight[GIndex] + SubRectValueWeight[GIndex ^ 32];
	GroupMemoryBarrierWithGroupSync();

	SubRectValueWeight[GIndex] =  SubRectValueWeight[GIndex] + SubRectValueWeight[GIndex ^ 64];
	GroupMemoryBarrierWithGroupSync();

	SubRectValueWeight[GIndex] =  SubRectValueWeight[GIndex] + SubRectValueWeight[GIndex ^ 128];
	GroupMemoryBarrierWithGroupSync();

	float LogLumAve = SubRectValueWeight[0].x /  SubRectValueWeight[0].y;
#else
	float LogLumAve = ComputeWeightedTextureAverageAlpha(PostprocessInput0,  EyeAdaptionSrcRect, WeightSlope);
#endif

	// Correct for [0,1] scaling
	LogLumAve = (LogLumAve - EyeAdaptation_HistogramBias) / EyeAdaptation_HistogramScale;
	
	// Convert LogLuminanceAverage to Average Intensity
	const float AverageSceneLuminance = OneOverPreExposure * exp2(LogLumAve);
	const float LumAve = AverageSceneLuminance * EyeAdaptation_CalibrationConstantInverse;

	const float ClampedLumAve = clamp(LumAve, EyeAdaptation_MinAverageLuminance, EyeAdaptation_MaxAverageLuminance);
	
	// The Exposure Scale (and thus intensity) used in the previous frame
	float ExposureScaleOld = EyeAdaptationTexture.Load(int3(0, 0, 0)).x;
	float LuminanceAveOld = EyeAdaptation_ExposureCompensation / (ExposureScaleOld != 0 ? ExposureScaleOld : 1.0f);

	// Time-based expoential blend of the intensity to allow the eye adaptation to ramp up over a few frames.
	float SmoothedLuminance = ComputeEyeAdaptation(LuminanceAveOld, ClampedLumAve, EyeAdaptation_DeltaWorldTime);

	float SmoothedExposureScale = 1.0f / max(0.0001f, SmoothedLuminance);
	float TargetExposureScale   = 1.0f / max(0.0001f, ClampedLumAve);
	
	// Output the number that will rescale the image intensity
	OutColor.x = EyeAdaptation_ExposureCompensation * SmoothedExposureScale;
	// Output the target value
	OutColor.y = EyeAdaptation_ExposureCompensation * TargetExposureScale;
	OutColor.z = AverageSceneLuminance;
	OutColor.w = 0;
		
#if COMPUTESHADER
	if(GIndex==0)
	{
		RWOutputComputeTex[uint2(0,0)] = OutColor;
	}
#endif
}