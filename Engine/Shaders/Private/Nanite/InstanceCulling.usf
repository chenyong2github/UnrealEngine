// Copyright Epic Games, Inc. All Rights Reserved.

#include "../Common.ush"
#include "../SceneData.ush"
#include "../WaveOpUtil.ush"
#include "../VirtualShadowMaps/PageAccessCommon.ush"

#include "NaniteDataDecode.ush"
#include "HZBCull.ush"
#include "WritePixel.ush"
#include "Imposter.ush"

void DrawImposter( FNaniteView NaniteView, FInstanceSceneData InstanceData, uint InstanceId, FScreenRect Rect )
{
	// High bit flags it as an imposter
	uint BaseCode = (1 << 31) | (1 << 7);
	uint RootClusterCode = ( InstanceId << 8 ) | BaseCode;
	uint RootClusterIndex = InstanceData.NaniteRuntimeResourceID & MAX_GPU_PAGES_MASK;

	for( int y = Rect.Pixels.y; y <= Rect.Pixels.w; y++ )
	{
		for( int x = Rect.Pixels.x; x <= Rect.Pixels.z; x++ )
		{
			uint2 PixelPos = uint2(x,y);

			// FIXME
			float2 Jitter = NaniteView.ViewToClip[2].xy * 0.5 * NaniteView.ViewSizeAndInvSize;
			int FrameRand = (int)(Jitter.x * 417.0f);

			float2 Noise = Rand3DPCG16( int3( PixelPos, FrameRand ) ).xy / 65535.0;

			FRay RayWorld;
			RayWorld.Origin = NaniteView.WorldCameraOrigin.xyz;

			float2 PixelClip = ( ( PixelPos + 0.5 ) * NaniteView.ViewSizeAndInvSize.zw - 0.5 ) * float2(2, -2);
			RayWorld.Direction = mul( float4( PixelClip, 0, 1 ), NaniteView.ClipToWorld ).xyz;
	
			FRay RayLocal;
			RayLocal.Origin		= mul( float4( RayWorld.Origin, 1 ),	InstanceData.WorldToLocal ).xyz;
			RayLocal.Direction	= mul( float4( RayWorld.Direction, 0 ),	InstanceData.WorldToLocal ).xyz;

			uint2 ImposterPixel = RayIntersectImposter( RayLocal, RootClusterIndex, InstanceData.LocalBoundsCenter, InstanceData.LocalBoundsExtent, Noise );

			if( ImposterPixel.y != 0 )
			{
				float Depth = asfloat( ImposterPixel.y );
				float4 HitClip = mul( float4( 0, 0, Depth, 1 ), NaniteView.ViewToClip );
				float DeviceZ = HitClip.z / HitClip.w;

				uint TriIndex = ImposterPixel.x;
				uint PixelValue = RootClusterCode + TriIndex;
				WritePixel( OutVisBuffer64, PixelValue, NaniteView.ViewRect.xy + PixelPos, DeviceZ, NaniteView, false );

				#if 0//DEBUG_VISUALIZE
					WritePixel( OutDbgBuffer64, 0, PixelPos, DeviceZ, NaniteView, false );
					InterlockedAdd(OutDbgBuffer32[ PixelPos ], 1);
				#endif
			}
		}
	}
}

//======================
// Instance culling
//
// Culls instances and outputs list of clusters to further cull.
//======================

uint NumInstances;
uint NumViews;
uint NumPrimaryViews;

StructuredBuffer<FInstanceDraw>			InInstanceDraws;
StructuredBuffer<uint>					InOccludedInstances;
Buffer<uint>							InOccludedInstancesArgs;

RWStructuredBuffer<uint>				OutOccludedInstances;
RWBuffer<uint>							OutOccludedInstancesArgs;
RWByteAddressBuffer						OutNodes;
RWStructuredBuffer<FPersistentState>	OutMainAndPostPassPersistentStates;

#if DEBUG_FLAGS
RWStructuredBuffer<FStats>				OutStatsBuffer;
#endif

[numthreads(64, 1, 1)]
void InstanceCull(
	uint DispatchIndex : SV_DispatchThreadID,
	uint GroupIndex : SV_GroupIndex)
{
#if DEBUG_FLAGS && CULLING_PASS == CULLING_PASS_OCCLUSION_POST
	if (DebugFlags & DEBUG_FLAG_WRITE_STATS && DispatchIndex == 0)
	{
		OutStatsBuffer[0].NumPostInstancesPreCull = InOccludedInstancesArgs[3];
	}
#endif

	const bool bIsPostPass = (CULLING_PASS == CULLING_PASS_OCCLUSION_POST);

#if NANITE_MULTI_VIEW && !INSTANCE_DRAW_LIST
	// Loop over each of the views
	for (uint ViewId = 0; ViewId < NumViews; ++ViewId)
	{
#else
	uint ViewId = 0;
#endif
	
#if !INSTANCE_DRAW_LIST || (CULLING_PASS == CULLING_PASS_OCCLUSION_POST)
	FNaniteView NaniteView = GetNaniteView( ViewId );
#endif

	bool bIsVisible = false;
	bool bWasOccluded = false;

#if CULLING_PASS == CULLING_PASS_NO_OCCLUSION || CULLING_PASS == CULLING_PASS_OCCLUSION_MAIN
	uint InstanceId = DispatchIndex;
	if (InstanceId < NumInstances)
	{
#if INSTANCE_DRAW_LIST
		ViewId = InInstanceDraws[ InstanceId ].ViewId;	
		InstanceId = InInstanceDraws[ InstanceId ].InstanceId;

		FNaniteView NaniteView = GetNaniteView( ViewId );
#endif // INSTANCE_DRAW_LIST

#else // !(CULLING_PASS == CULLING_PASS_NO_OCCLUSION || CULLING_PASS == CULLING_PASS_OCCLUSION_MAIN)
	uint InstanceId = 0;
	if (DispatchIndex < InOccludedInstancesArgs[3])
	{
		InstanceId = InOccludedInstances[DispatchIndex];
#endif // CULLING_PASS == CULLING_PASS_NO_OCCLUSION || CULLING_PASS == CULLING_PASS_OCCLUSION_MAIN
		
		FInstanceSceneData InstanceData = GetInstanceData( InstanceId );
		const uint RuntimeResourceID = InstanceData.NaniteRuntimeResourceID;

		// Depth clipping should only be disabled with orthographic projections
		const bool bNearClip = (NEAR_CLIP != 0);

#if DEBUG_FLAGS && COMPILER_PSSL
		const bool bSkipBoxCullFrustum = (DebugFlags & DEBUG_FLAG_CULL_FRUSTUM_BOX) == 0;
		const bool bSkipBoxCullHZB     = (DebugFlags & DEBUG_FLAG_CULL_HZB_BOX) == 0;
#else
		const bool bSkipBoxCullFrustum = false;
		const bool bSkipBoxCullHZB     = false;
#endif

		float4x4 LocalToTranslatedWorld = InstanceData.LocalToWorld;
		LocalToTranslatedWorld[3].xyz += NaniteView.PreViewTranslation.xyz;
		float4x4 LocalToClip = mul( LocalToTranslatedWorld, NaniteView.TranslatedWorldToClip );

		float BoundsRadiusSq = dot(InstanceData.LocalBoundsExtent, InstanceData.LocalBoundsExtent);
		bIsVisible = (InstanceData.PrimitiveId != 0xFFFFFFFFu && RuntimeResourceID != 0xFFFFFFFFu) && BoundsRadiusSq > NaniteView.MinBoundsRadiusSq; // Only process valid Nanite primitives

		FFrustumCullData Cull;
		
#if CULLING_PASS == CULLING_PASS_NO_OCCLUSION || CULLING_PASS == CULLING_PASS_OCCLUSION_MAIN
		BRANCH
		if( bIsVisible )
		{
			Cull = BoxCullFrustum(InstanceData.LocalBoundsCenter, InstanceData.LocalBoundsExtent, LocalToClip, bNearClip, bSkipBoxCullFrustum);
			bIsVisible = Cull.bIsVisible;
		}

		BRANCH
		if( bIsVisible )
		{
			if( CULLING_PASS == CULLING_PASS_OCCLUSION_MAIN )
			{
				float4x4 LocalToPrevTranslatedWorld = InstanceData.LocalToWorld;
				LocalToPrevTranslatedWorld[ 3 ].xyz += NaniteView.PrevPreViewTranslation.xyz;
				float4x4 LocalToPrevClip = mul(LocalToPrevTranslatedWorld, NaniteView.PrevTranslatedWorldToClip);

				FFrustumCullData PrevCull = BoxCullFrustum(InstanceData.LocalBoundsCenter, InstanceData.LocalBoundsExtent, LocalToPrevClip, bNearClip, bSkipBoxCullFrustum);
				
				BRANCH
				if (PrevCull.bIsVisible && !PrevCull.bCrossesNearPlane)
				{
					FScreenRect PrevRect = GetScreenRect( HZBViewSize, PrevCull, 4 );
					bWasOccluded = bSkipBoxCullHZB ? false : !IsVisibleHZB( PrevRect, true );
				}

				if(!PrevCull.bIsVisible)
					bWasOccluded = true;	// Not in previous frame. Assume occluded instead of occluder. Enables disocclusion hack.

#if CULLING_PASS == CULLING_PASS_OCCLUSION_MAIN
				if( bWasOccluded )
				{
					uint OccludedInstanceOffset = 0;
					WaveInterlockedAddScalarInGroups( OutOccludedInstancesArgs[3], OutOccludedInstancesArgs[0], 64, 1, OccludedInstanceOffset );
					OutOccludedInstances[ OccludedInstanceOffset ] = InstanceId;
				}
#endif
			}
		}
#elif CULLING_PASS == CULLING_PASS_OCCLUSION_POST
		BRANCH
		if( bIsVisible )
		{
			Cull = BoxCullFrustum(InstanceData.LocalBoundsCenter, InstanceData.LocalBoundsExtent, LocalToClip, bNearClip, bSkipBoxCullFrustum);

			BRANCH
			if (Cull.bIsVisible && !Cull.bCrossesNearPlane)
			{
				FScreenRect Rect = GetScreenRect( NaniteView.ViewSizeAndInvSize.xy, Cull, 4 );
				bWasOccluded = bSkipBoxCullHZB ? false : !IsVisibleHZB( Rect, true );
			}
			
			bIsVisible = !bWasOccluded;
		}
#endif

#if !INSTANCE_DRAW_LIST
		if( bIsVisible && !bWasOccluded )
		{
			FScreenRect Rect = GetScreenRect( NaniteView.ViewSizeAndInvSize.xy, Cull, 4 );

			if( all( Rect.Pixels.zw - Rect.Pixels.xy < 6 ) )
			{
				DrawImposter( NaniteView, InstanceData, InstanceId, Rect );
				bIsVisible = false;
			}
		}
#endif
	}

	if( bIsVisible && !bWasOccluded )
	{
		uint NodeOffset = 0;
		uint PersistentStateIndex = ( CULLING_PASS == CULLING_PASS_OCCLUSION_POST );
		WaveInterlockedAddScalar_( OutMainAndPostPassPersistentStates[ PersistentStateIndex ].WriteOffset, 1, NodeOffset );
		WaveInterlockedAddScalar(  OutMainAndPostPassPersistentStates[ PersistentStateIndex ].NumActive, 1 );

#if DEBUG_FLAGS
		if (DebugFlags & DEBUG_FLAG_WRITE_STATS)
		{
		#if CULLING_PASS == CULLING_PASS_OCCLUSION_POST
			WaveInterlockedAddScalar( OutStatsBuffer[0].NumPostInstancesPostCull, 1 );
		#else
			WaveInterlockedAddScalar( OutStatsBuffer[0].NumMainInstancesPostCull, 1 );
		#endif
		}
#endif

		uint Flags = CULLING_FLAG_TEST_LOD;
#if CULLING_PASS == CULLING_PASS_OCCLUSION_POST
		Flags |= CULLING_FLAG_FROM_DISOCCLUDED_INSTANCE;
#endif
		
		FVisibleNode Node;
		Node.Flags = Flags;
		Node.ViewId = ViewId;
		Node.InstanceId = InstanceId;
		Node.NodeIndex = 0;
		Node.EnabledBitmask[0] = 0xFFFFFFFFu;
		Node.EnabledBitmask[1] = 0xFFFFFFFFu;
		StoreVisibleNode( OutNodes, NodeOffset, Node, bIsPostPass );
	}
#if NANITE_MULTI_VIEW && !INSTANCE_DRAW_LIST
	}
#endif // NANITE_MULTI_VIEW && !INSTANCE_DRAW_LIST
}

[numthreads(64, 1, 1)]
void InstanceCullVSM(
	uint DispatchIndex : SV_DispatchThreadID,
	uint GroupIndex : SV_GroupIndex)
{
	// Depth clipping should only be disabled with orthographic projections
	const bool bNearClip = ( NEAR_CLIP != 0 );

#if DEBUG_FLAGS && COMPILER_PSSL
	const bool bSkipBoxCullFrustum = (DebugFlags & DEBUG_FLAG_CULL_FRUSTUM_BOX) == 0;
	const bool bSkipBoxCullHZB     = (DebugFlags & DEBUG_FLAG_CULL_HZB_BOX) == 0;
#else
	const bool bSkipBoxCullFrustum = false;
	const bool bSkipBoxCullHZB     = false;
#endif

	uint InstanceId = DispatchIndex;
	FInstanceSceneData InstanceData = GetInstanceData( InstanceId );
	const uint RuntimeResourceID = InstanceData.NaniteRuntimeResourceID;

	const bool bActive = InstanceId < NumInstances && InstanceData.PrimitiveId != 0xFFFFFFFFu && RuntimeResourceID != 0xFFFFFFFFu; // Only process valid Nanite primitives

	const bool bHasMoved = GetGPUSceneFrameNumber() == InstanceData.LastUpdateSceneFrameNumber;

	// Loop over each of the views
	for (uint PrimaryViewId = 0; PrimaryViewId < NumPrimaryViews; ++PrimaryViewId)
	{
		uint VisibleMipsMask = 0;
		if( bActive )
		{
			FNaniteView NaniteView = GetNaniteView( PrimaryViewId );
			const uint2 TargetViewSize = uint2( NaniteView.ViewSizeAndInvSize.xy );
			
			float4x4 LocalToTranslatedWorld = InstanceData.LocalToWorld;
			LocalToTranslatedWorld[ 3 ].xyz += NaniteView.PreViewTranslation.xyz;
			float4x4 LocalToClip = mul( LocalToTranslatedWorld, NaniteView.TranslatedWorldToClip );

			FFrustumCullData Cull = BoxCullFrustum( InstanceData.LocalBoundsCenter, InstanceData.LocalBoundsExtent, LocalToClip, bNearClip, bSkipBoxCullFrustum );
			
			for(uint TargetMipLevel = 0; TargetMipLevel < NaniteView.TargetNumMipLevels; TargetMipLevel++)
			{
				uint MipViewId = TargetMipLevel * NumPrimaryViews + PrimaryViewId;
				float2 ViewSize = GetNaniteView(MipViewId).ViewSizeAndInvSize.xy;
				int2 ViewMin = GetNaniteView(MipViewId).ViewRect.xy;

				// TODO: minor optimization possible, but need to duplicate setup from CullRasterize for virtual targets
				//float2 ViewSize = float2( ( TargetViewSize + ( 1u << TargetMipLevel ) - 1u ) >> TargetMipLevel );

				FScreenRect Rect = GetScreenRect( ViewSize, Cull, 4 );

				bool bVisible = Cull.bIsVisible && Rect.bOverlapsPixelCenter;
		
				BRANCH
				if( bVisible && !Cull.bCrossesNearPlane )
				{
					bVisible = OverlapsAnyValidPage( NaniteView.TargetLayerIndex, TargetMipLevel, ViewMin, Rect, bHasMoved );
				}

				BRANCH
				if( bVisible )
				{
					float4x4 LocalToPrevTranslatedWorld = InstanceData.LocalToWorld;
					LocalToPrevTranslatedWorld[ 3 ].xyz += NaniteView.PrevPreViewTranslation.xyz;
					float4x4 LocalToPrevClip = mul(LocalToPrevTranslatedWorld, NaniteView.PrevTranslatedWorldToClip);

					FFrustumCullData PrevCull = BoxCullFrustum(InstanceData.LocalBoundsCenter, InstanceData.LocalBoundsExtent, LocalToPrevClip, bNearClip, bSkipBoxCullFrustum);
				
					BRANCH
					if( PrevCull.bIsVisible && !PrevCull.bCrossesNearPlane )
					{
						FScreenRect PrevRect = GetScreenRect( ViewSize, PrevCull, 4 );
						bVisible = bSkipBoxCullHZB ? true : IsVisibleHZB( NaniteView.TargetPrevLayerIndex, TargetMipLevel, PrevRect );
					}
				}

				if( bVisible )
				{
					VisibleMipsMask |= ( 1u << TargetMipLevel );
				}
			}
		}

		uint NodeOffset = 0;

		BRANCH
		if( VisibleMipsMask )
		{
			uint NumNodes = countbits( VisibleMipsMask );
			WaveInterlockedAdd_( OutMainAndPostPassPersistentStates[0].WriteOffset, NumNodes, NodeOffset );
			WaveInterlockedAdd(  OutMainAndPostPassPersistentStates[0].NumActive, NumNodes );

	#if DEBUG_FLAGS
			if (DebugFlags & DEBUG_FLAG_WRITE_STATS)
			{
				WaveInterlockedAdd(OutStatsBuffer[0].NumMainInstancesPostCull, NumNodes);
			}
	#endif
		}

		// Output visible nodes. Get compaction for free by only looping over set bits in VisibleMipsMask.
		// Iteration count is equal to the maximum lane output count.
		while( VisibleMipsMask )
		{
			uint TargetMipLevel = firstbitlow( VisibleMipsMask );	// Jump to next set bit.
			VisibleMipsMask &= VisibleMipsMask - 1u;				// Remove bottom bit.

			uint MipViewId = TargetMipLevel * NumPrimaryViews + PrimaryViewId;

			FVisibleNode Node;
			Node.Flags = CULLING_FLAG_TEST_LOD;
			Node.ViewId = MipViewId;
			Node.InstanceId = InstanceId;
			Node.NodeIndex = 0;
			Node.EnabledBitmask[0] = 0xFFFFFFFFu;
			Node.EnabledBitmask[1] = 0xFFFFFFFFu;
			StoreVisibleNode( OutNodes, NodeOffset, Node, false );
			NodeOffset++;
		}
	}
}

