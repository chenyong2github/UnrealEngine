// Copyright Epic Games, Inc. All Rights Reserved.

#pragma once

#include "../VertexFactoryCommon.ush"
#include "../LightmapData.ush"
#include "NaniteDataDecode.ush"
#include "NaniteAttributeDecode.ush"

#ifndef VIRTUAL_TEXTURE_TARGET
#define VIRTUAL_TEXTURE_TARGET 0
#endif

#ifndef NANITE_USE_HW_BARYCENTRICS
#define NANITE_USE_HW_BARYCENTRICS 0
#endif

// Nanite material evaluation is deferred to a screenspace pass sampling the visibility buffer,
// so the 'interpolants' used in the GBuffer pass are almost all generated in the PixelShader, instead of exported from VS.
// FNaniteFullscreenVSToPS is the struct containing what actually needs to be passed between VS and PS in the Nanite GBuffer pass.
struct FVertexFactoryInterpolantsVSToPS
{
#if NEEDS_LIGHTMAP_COORDINATE
	nointerpolation float4 LightMapCoordinate	: TEXCOORD3;
	nointerpolation float4 LightMapCoordinateDDX: TEXCOORD4;
	nointerpolation float4 LightMapCoordinateDDY: TEXCOORD5;
#endif
#if VF_USE_PRIMITIVE_SCENE_DATA && NEEDS_LIGHTMAP_COORDINATE
	nointerpolation uint LightmapDataIndex		: LIGHTMAP_ID;
#endif
#if INSTANCED_STEREO
	nointerpolation uint EyeIndex				: PACKED_EYE_INDEX;
#endif
	nointerpolation uint ViewIndex				: PACKED_VIEW_INDEX;
};

#if IS_NANITE_RASTER_PASS

// TODO: PROG_RASTER

struct FVertexFactoryInput
{
};

struct FVertexFactoryIntermediates
{
	float3x3 TangentToLocal;
};

FVertexFactoryIntermediates GetVertexFactoryIntermediates(FVertexFactoryInput Input)
{
	FVertexFactoryIntermediates Intermediates = (FVertexFactoryIntermediates)0;
	return Intermediates;
};

float3x3 VertexFactoryGetTangentToLocal(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	return Intermediates.TangentToLocal;
}

FMaterialVertexParameters GetMaterialVertexParameters(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, float3 WorldPosition, float3x3 TangentToLocal)
{
	FMaterialVertexParameters Result = (FMaterialVertexParameters)0;

	return Result;
}

#endif // IS_NANITE_RASTER_PASS

#if NEEDS_LIGHTMAP_COORDINATE

void GetLightMapCoordinates(FVertexFactoryInterpolantsVSToPS Interpolants, out float2 LightmapUV0, out float2 LightmapUV1, out uint LightmapDataIndex)
{
	LightmapUV0 = Interpolants.LightMapCoordinate.xy * float2(1.0, 0.5);
	LightmapUV1 = LightmapUV0 + float2(0.0, 0.5);

#if VF_USE_PRIMITIVE_SCENE_DATA
	LightmapDataIndex = Interpolants.LightmapDataIndex;
#else
	LightmapDataIndex = 0;
#endif
}

void GetLightMapCoordinates(FVertexFactoryInterpolantsVSToPS Interpolants, out FloatDeriv2 LightmapUV0, out FloatDeriv2 LightmapUV1, out uint LightmapDataIndex)
{
	LightmapUV0 = ConstructFloatDeriv2(	Interpolants.LightMapCoordinate.xy * float2(1.0, 0.5),
										Interpolants.LightMapCoordinateDDX.xy * float2(1.0, 0.5),
										Interpolants.LightMapCoordinateDDY.xy * float2(1.0, 0.5));

	LightmapUV1 = LightmapUV0;
	LightmapUV1.Value += float2(0.0, 0.5);

#if VF_USE_PRIMITIVE_SCENE_DATA
	LightmapDataIndex = Interpolants.LightmapDataIndex;
#else
	LightmapDataIndex = 0;
#endif
}

void GetShadowMapCoordinate(FVertexFactoryInterpolantsVSToPS Interpolants, out float2 ShadowMapCoordinate, out uint LightmapDataIndex)
{
#if VF_USE_PRIMITIVE_SCENE_DATA
	LightmapDataIndex = Interpolants.LightmapDataIndex;
#else
	LightmapDataIndex = 0;
#endif
	ShadowMapCoordinate = Interpolants.LightMapCoordinate.zw;
}

void GetShadowMapCoordinate(FVertexFactoryInterpolantsVSToPS Interpolants, out FloatDeriv2 ShadowMapCoordinate, out uint LightmapDataIndex)
{
#if VF_USE_PRIMITIVE_SCENE_DATA
	LightmapDataIndex = Interpolants.LightmapDataIndex;
#else
	LightmapDataIndex = 0;
#endif
	ShadowMapCoordinate = ConstructFloatDeriv2(	Interpolants.LightMapCoordinate.zw,
												Interpolants.LightMapCoordinateDDX.zw,
												Interpolants.LightMapCoordinateDDY.zw);
}

void SetLightMapCoordinate(inout FVertexFactoryInterpolantsVSToPS Interpolants, float2 InLightMapCoordinate, float2 InShadowMapCoordinate)
{
	Interpolants.LightMapCoordinate.xy = InLightMapCoordinate;
	Interpolants.LightMapCoordinate.zw = InShadowMapCoordinate;
	Interpolants.LightMapCoordinateDDX = 0;
	Interpolants.LightMapCoordinateDDY = 0;
}

void SetLightMapCoordinate(inout FVertexFactoryInterpolantsVSToPS Interpolants, FloatDeriv2 InLightMapCoordinate, FloatDeriv2 InShadowMapCoordinate)
{
	Interpolants.LightMapCoordinate		= float4(InLightMapCoordinate.Value, InShadowMapCoordinate.Value);
	Interpolants.LightMapCoordinateDDX	= float4(InLightMapCoordinate.Ddx, InShadowMapCoordinate.Ddy);
	Interpolants.LightMapCoordinateDDY	= float4(InLightMapCoordinate.Ddy, InShadowMapCoordinate.Ddy);
}

void SetLightMapDataIndex(inout FVertexFactoryInterpolantsVSToPS Interpolants, uint LightmapDataIndex)
{
#if VF_USE_PRIMITIVE_SCENE_DATA
	Interpolants.LightmapDataIndex = LightmapDataIndex;
#endif
}

#endif // NEEDS_LIGHTMAP_COORDINATE

void SetVertexParameterInstanceData(inout FMaterialVertexParameters VertexParameters, FInstanceSceneData InstanceData)
{
	VertexParameters.PrimitiveId			= InstanceData.PrimitiveId;
	VertexParameters.InstanceLocalToWorld	= InstanceData.LocalToWorld;
	VertexParameters.InstanceWorldToLocal	= InstanceData.WorldToLocal;
	VertexParameters.PrevFrameLocalToWorld	= InstanceData.PrevLocalToWorld;
#if USES_PER_INSTANCE_CUSTOM_DATA
	VertexParameters.CustomDataOffset		= InstanceData.CustomDataOffset;
	VertexParameters.CustomDataCount		= InstanceData.CustomDataCount;
#endif
#if USES_PER_INSTANCE_RANDOM
	// X = per-instance random, Y = per-instance fade out amount, Z = hide/show flag, W = dither fade cutoff
	VertexParameters.PerInstanceParams		= float4(InstanceData.RandomID, 0.0f, 0.0f, 0.0f);
#endif
}

void SetVertexParameterAttributeData(inout FMaterialVertexParameters VertexParameters, FNaniteRawAttributeData RawAttributeData, float4x4 LocalToTranslatedWorld, float3x3 LocalToWorld, uint VertIndex, float3 PointLocalNoWPO)
{
	VertexParameters.WorldPosition		= mul(float4(PointLocalNoWPO, 1), LocalToTranslatedWorld).xyz;
	VertexParameters.TangentToWorld		= mul(GetTangentBasis(RawAttributeData.TangentZ[VertIndex]), LocalToWorld);

#if INTERPOLATE_VERTEX_COLOR
	#if NANITE_USE_UNCOMPRESSED_VERTEX_DATA
		VertexParameters.VertexColor	= float4(RawAttributeData.Color[VertIndex]) * (1.0f / 255.0f);
	#else
		VertexParameters.VertexColor	= (RawAttributeData.ColorMin + float4(RawAttributeData.ColorDelta[VertIndex])) * (1.0f / 255.0f);
	#endif
#else
	// Coerce compiler into DCE as much code as possible.
	VertexParameters.VertexColor		= float4(1, 1, 1, 1);
#endif
#if NUM_MATERIAL_TEXCOORDS_VERTEX
	UNROLL
	for (uint TexCoordIndex = 0; TexCoordIndex < NUM_MATERIAL_TEXCOORDS_VERTEX; ++TexCoordIndex)
	{
		// Protect against case where Nanite max UV count is lower than what the material may define.
		if (TexCoordIndex < NANITE_MAX_UVS)
		{
			VertexParameters.TexCoords[TexCoordIndex] = RawAttributeData.TexCoords[VertIndex][TexCoordIndex];
		}
	}
#endif
}

// Group of transforms needed to transform a Nanite vertex
struct FNaniteVertTransforms
{
	float4x4 LocalToTranslatedWorld;
	float4x4 PrevLocalToTranslatedWorld;
	float4x4 LocalToClip;
	float4x4 TranslatedWorldToClip;
	float3x3 LocalToWorldNoScale;
	float3x3 PrevLocalToWorldNoScale;
	float3x3 WorldToLocalVector;
	float3x3 PrevWorldToLocalVector;
};

// Post-transformed Nanite triangle data
struct FNaniteTransformedTri
{
	float3 PointLocal[3];
	float3 PrevPointLocal[3];
	float3 PointWorld[3];
	float4 PointClip[3];
	FNaniteRawAttributeData RawAttributeData;
};

FNaniteVertTransforms CalculateNaniteVertexTransforms(FInstanceSceneData InstanceData, FInstanceDynamicData InstanceDynamicData, FNaniteView NaniteView)
{
	const float4x4 LocalToTranslatedWorld = InstanceDynamicData.LocalToTranslatedWorld;
	const float4x4 PrevLocalToTranslatedWorld = InstanceDynamicData.PrevLocalToTranslatedWorld;
	const float4x4 LocalToClip = InstanceDynamicData.LocalToClip;
	const float4x4 TranslatedWorldToClip = NaniteView.TranslatedWorldToClip;
	const float3 InvNonUniformScale = InstanceData.InvNonUniformScale;

	// Should be Pow2(InvScale) but that requires renormalization
	float3x3 LocalToWorldNoScale = (float3x3)LocalToTranslatedWorld;
	LocalToWorldNoScale[0] *= InvNonUniformScale.x;
	LocalToWorldNoScale[1] *= InvNonUniformScale.y;
	LocalToWorldNoScale[2] *= InvNonUniformScale.z;

	float3x3 PrevLocalToWorldNoScale = (float3x3)PrevLocalToTranslatedWorld;
	PrevLocalToWorldNoScale[0] *= InvNonUniformScale.x;
	PrevLocalToWorldNoScale[1] *= InvNonUniformScale.y;
	PrevLocalToWorldNoScale[2] *= InvNonUniformScale.z;

	float3x3 WorldToLocalVector = LWCToFloat3x3(InstanceData.WorldToLocal);
	
	// TODO: We need PrevWorldToLocal here, but we don't have it
	const float3 SqInvNonUniformScale = Pow2(InvNonUniformScale);
	float3x3 PrevWorldToLocalVector = LWCToFloat3x3(InstanceData.PrevLocalToWorld);
	PrevWorldToLocalVector[0] *= SqInvNonUniformScale.x;
	PrevWorldToLocalVector[1] *= SqInvNonUniformScale.y;
	PrevWorldToLocalVector[2] *= SqInvNonUniformScale.z;
	PrevWorldToLocalVector = transpose(PrevWorldToLocalVector);

	FNaniteVertTransforms Transforms;
	Transforms.LocalToTranslatedWorld		= LocalToTranslatedWorld;
	Transforms.PrevLocalToTranslatedWorld	= PrevLocalToTranslatedWorld;
	Transforms.LocalToClip					= LocalToClip;
	Transforms.TranslatedWorldToClip		= TranslatedWorldToClip;
	Transforms.LocalToWorldNoScale			= LocalToWorldNoScale;
	Transforms.PrevLocalToWorldNoScale		= PrevLocalToWorldNoScale;
	Transforms.WorldToLocalVector			= WorldToLocalVector;
	Transforms.PrevWorldToLocalVector		= PrevWorldToLocalVector;

	return Transforms;
}

FNaniteTransformedTri TransformNaniteTriangle(FInstanceSceneData InstanceData, FNaniteVertTransforms Transforms, float3 PointLocalNoWPO[3], FNaniteRawAttributeData RawAttributeData)
{
	FNaniteTransformedTri Tri = (FNaniteTransformedTri)0;

	Tri.RawAttributeData = RawAttributeData;

	// TODO: PROG_RASTER: Optimize
#if USES_WORLD_POSITION_OFFSET
	UNROLL_N(3)
	for (int i = 0; i < 3; ++i)
	{
		FMaterialVertexParameters VertexParameters = (FMaterialVertexParameters)0;
		SetVertexParameterInstanceData(VertexParameters, InstanceData);
		SetVertexParameterAttributeData(VertexParameters, RawAttributeData, Transforms.LocalToTranslatedWorld, Transforms.LocalToWorldNoScale, i, PointLocalNoWPO[i]);

		FMaterialVertexParameters PrevVertexParameters = (FMaterialVertexParameters)0;
		SetVertexParameterInstanceData(PrevVertexParameters, InstanceData);
		SetVertexParameterAttributeData(PrevVertexParameters, RawAttributeData, Transforms.PrevLocalToTranslatedWorld, Transforms.PrevLocalToWorldNoScale, i, PointLocalNoWPO[i]);

		const float3 WorldPositionOffset = GetMaterialWorldPositionOffset(VertexParameters);
		const float3 PrevWorldPositionOffset = GetMaterialPreviousWorldPositionOffset(PrevVertexParameters);
		const float3 LocalOffset = mul(WorldPositionOffset, Transforms.WorldToLocalVector);
		const float3 PrevLocalOffset = mul(PrevWorldPositionOffset, Transforms.PrevWorldToLocalVector);

		Tri.PointLocal[i]		= PointLocalNoWPO[i] + LocalOffset;
		Tri.PrevPointLocal[i]	= PointLocalNoWPO[i] + PrevLocalOffset;
		Tri.PointWorld[i]		= VertexParameters.WorldPosition + WorldPositionOffset;
		Tri.PointClip[i]		= mul(float4(Tri.PointWorld[i], 1), Transforms.TranslatedWorldToClip);
	}
#else
	UNROLL_N(3)
	for (int i = 0; i < 3; ++i)
	{
		Tri.PointLocal[i]		= PointLocalNoWPO[i];
		Tri.PrevPointLocal[i]	= PointLocalNoWPO[i];
		Tri.PointWorld[i]		= mul(float4(Tri.PointLocal[i], 1), Transforms.LocalToTranslatedWorld).xyz;
		Tri.PointClip[i]		= mul(float4(Tri.PointLocal[i], 1), Transforms.LocalToClip);
	}
#endif // USES WORLD_POSITION_OFFSET

	return Tri;
}

FMaterialPixelParameters FetchNaniteMaterialPixelParameters(FInstanceSceneData InstanceData, FInstanceDynamicData InstanceDynamicData, FNaniteView NaniteView, FNaniteTransformedTri Tri, FCluster Cluster, FBarycentrics Barycentrics, inout FVertexFactoryInterpolantsVSToPS Interpolants, inout float4 SvPosition)
{
	FMaterialPixelParameters Result = (FMaterialPixelParameters)0;

	const float SignTest = dot(cross(Tri.PointClip[1].xyw - Tri.PointClip[0].xyw, Tri.PointClip[2].xyw - Tri.PointClip[0].xyw), Tri.PointClip[0].xyw);
	Result.TwoSidedSign = CondMask(SignTest >= 0.0f, -1.0f, 1.0f);

	const FNaniteAttributeData AttributeData = GetAttributeData(
		Tri.RawAttributeData,
		Cluster,
		Tri.PointLocal[0],
		Tri.PointLocal[1],
		Tri.PointLocal[2],
		Barycentrics,
		InstanceData,
		NUM_TEX_COORD_INTERPOLATORS
	);

#if INTERPOLATE_VERTEX_COLOR
	Result.VertexColor = AttributeData.VertexColor;
	Result.VertexColor_DDX = AttributeData.VertexColor_DDX;
	Result.VertexColor_DDY = AttributeData.VertexColor_DDY;
#else
	// Coerce compiler into DCE as much code as possible.
	Result.VertexColor = float4(1, 1, 1, 1);
	Result.VertexColor_DDX = 0.0f;
	Result.VertexColor_DDY = 0.0f;
#endif

	Result.TangentToWorld = AttributeData.TangentToWorld;
	Result.UnMirrored = AttributeData.UnMirrored;

#if NUM_TEX_COORD_INTERPOLATORS > 0
	UNROLL
	for (uint TexCoordIndex = 0; TexCoordIndex < NUM_TEX_COORD_INTERPOLATORS; TexCoordIndex++)
	{
		// Protect against case where Nanite max UV count is lower than what the material may define.
		if (TexCoordIndex < NANITE_MAX_UVS)
		{
			Result.TexCoords[TexCoordIndex] = AttributeData.TexCoords[TexCoordIndex];
			Result.TexCoords_DDX[TexCoordIndex] = AttributeData.TexCoords_DDX[TexCoordIndex];
			Result.TexCoords_DDY[TexCoordIndex] = AttributeData.TexCoords_DDY[TexCoordIndex];
		}
	}
#endif

	const float3 PrevPointLocal = Barycentrics.UVW.x * Tri.PrevPointLocal[0] + Barycentrics.UVW.y * Tri.PrevPointLocal[1] + Barycentrics.UVW.z * Tri.PrevPointLocal[2];
	Result.PrevScreenPosition = mul( float4(PrevPointLocal, 1), InstanceDynamicData.PrevLocalToClip );
	//Result.PrevScreenPosition = float4( Result.PrevScreenPosition.xyz / Result.PrevScreenPosition.w, 1 );

	const float3 PointWorld = Barycentrics.UVW.x * Tri.PointWorld[0] + Barycentrics.UVW.y * Tri.PointWorld[1] + Barycentrics.UVW.z * Tri.PointWorld[2];
	
	Result.WorldPosition_CamRelative = PointWorld;
	Result.WorldPosition_DDX = Barycentrics.UVW_dx.x * Tri.PointWorld[0] + Barycentrics.UVW_dx.y * Tri.PointWorld[1] + Barycentrics.UVW_dx.z * Tri.PointWorld[2];
	Result.WorldPosition_DDY = Barycentrics.UVW_dy.x * Tri.PointWorld[0] + Barycentrics.UVW_dy.y * Tri.PointWorld[1] + Barycentrics.UVW_dy.z * Tri.PointWorld[2];

	// Update screen W and all screen derivatives. This is rarely used and will be dead code eliminated most of the time.
	{
		const float2 PointClipZW = Barycentrics.UVW.x * Tri.PointClip[0].zw + Barycentrics.UVW.y * Tri.PointClip[1].zw + Barycentrics.UVW.z * Tri.PointClip[2].zw;
		SvPosition.w = PointClipZW.y;

		float2 Z_DDX_DDY = float2(	Barycentrics.UVW_dx.x * Tri.PointClip[0].zw.x + Barycentrics.UVW_dx.y * Tri.PointClip[1].zw.x + Barycentrics.UVW_dx.z * Tri.PointClip[2].zw.x,
									Barycentrics.UVW_dy.x * Tri.PointClip[0].zw.x + Barycentrics.UVW_dy.y * Tri.PointClip[1].zw.x + Barycentrics.UVW_dy.z * Tri.PointClip[2].zw.x);
		float2 W_DDX_DDY = float2(	Barycentrics.UVW_dx.x * Tri.PointClip[0].zw.y + Barycentrics.UVW_dx.y * Tri.PointClip[1].zw.y + Barycentrics.UVW_dx.z * Tri.PointClip[2].zw.y,
									Barycentrics.UVW_dy.x * Tri.PointClip[0].zw.y + Barycentrics.UVW_dy.y * Tri.PointClip[1].zw.y + Barycentrics.UVW_dy.z * Tri.PointClip[2].zw.y);

		// PPZ = Z / W
		// PPZ' = (Z'W - ZW')/W^2
		float2 PPZ_DDX_DDY = (Z_DDX_DDY * PointClipZW.y - PointClipZW.x * W_DDX_DDY) / (PointClipZW.y * PointClipZW.y);
		SvPositionToResolvedScreenPositionDeriv(SvPosition, PPZ_DDX_DDY, W_DDX_DDY, Result.ScreenPosition, Result.ScreenPosition_DDX, Result.ScreenPosition_DDY);
	}
	
#if USE_PARTICLE_SUBUVS && NUM_TEX_COORD_INTERPOLATORS > 0
	// Output TexCoord0 for when previewing materials that use ParticleSubUV.
	Result.Particle.SubUVCoords[0] = Result.TexCoords[0];
	Result.Particle.SubUVCoords[1] = Result.TexCoords[0];
#endif

	// Required for previewing materials that use ParticleColor
	Result.Particle.Color = half4(1, 1, 1, 1);

#if USES_PER_INSTANCE_RANDOM
	// X = per-instance random, Y = per-instance fade out amount, Z = hide/show flag, W = dither fade cutoff
	Result.PerInstanceParams = float4(InstanceData.RandomID, 0.0f, 0.0f, 0.0f);
#endif

#if NEEDS_LIGHTMAP_COORDINATE
	const uint LightMapDataIndex = PrimitiveData.LightmapDataIndex;
	const uint LightMapUVIndex   = PrimitiveData.LightmapUVIndex;

	float2 LightMapCoordinateInput;
	float2 LightMapCoordinateInput_DDX;
	float2 LightMapCoordinateInput_DDY;
	if (LightMapUVIndex < NUM_TEX_COORD_INTERPOLATORS)
	{
		LightMapCoordinateInput = AttributeData.TexCoords[LightMapUVIndex];
		LightMapCoordinateInput_DDX = AttributeData.TexCoords_DDX[LightMapUVIndex];
		LightMapCoordinateInput_DDY = AttributeData.TexCoords_DDY[LightMapUVIndex];
	}
	else
	{
		FTexCoord TexCoord = GetTexCoord(Cluster, TriIndices, Barycentrics, LightMapUVIndex);
		LightMapCoordinateInput = TexCoord.Value;
		LightMapCoordinateInput_DDX = TexCoord.DDX;
		LightMapCoordinateInput_DDY = TexCoord.DDY;
	}

	const bool bHasPerInstanceCoordinateScaleBias = (InstanceData.Flags & INSTANCE_SCENE_DATA_FLAG_HAS_LIGHTSHADOW_UV_BIAS);

	const float4 LightMapCoordinateScaleBias = GetLightmapData(LightMapDataIndex).LightMapCoordinateScaleBias;
	const float2 InstanceLightMapScaleBias = CondMask(bHasPerInstanceCoordinateScaleBias, InstanceData.LightMapAndShadowMapUVBias.xy, LightMapCoordinateScaleBias.zw);
	FloatDeriv2 LightMapCoordinate = ConstructFloatDeriv2(	LightMapCoordinateInput * LightMapCoordinateScaleBias.xy + InstanceLightMapScaleBias,
															LightMapCoordinateInput_DDX * LightMapCoordinateScaleBias.xy,
															LightMapCoordinateInput_DDY * LightMapCoordinateScaleBias.xy);

	FloatDeriv2 ShadowMapCoordinate = (FloatDeriv2)0;
#if STATICLIGHTING_TEXTUREMASK
	const float4 ShadowMapCoordinateScaleBias = GetLightmapData(LightMapDataIndex).ShadowMapCoordinateScaleBias;
	const float2 InstanceShadowMapScaleBias = CondMask(bHasPerInstanceCoordinateScaleBias, InstanceData.LightMapAndShadowMapUVBias.zw, ShadowMapCoordinateScaleBias.zw);
	ShadowMapCoordinate = ConstructFloatDeriv2(	LightMapCoordinateInput * ShadowMapCoordinateScaleBias.xy + InstanceShadowMapScaleBias,
												LightMapCoordinateInput_DDX * ShadowMapCoordinateScaleBias.xy,
												LightMapCoordinateInput_DDY * ShadowMapCoordinateScaleBias.xy);
#endif

#if LIGHTMAP_UV_ACCESS
	// Store unscaled/unbiased lightmap UVs
	Result.LightmapUVs = LightMapCoordinateInput;
	Result.LightmapUVs_DDX = LightMapCoordinateInput_DDX;
	Result.LightmapUVs_DDY = LightMapCoordinateInput_DDY;
#endif

	SetLightMapCoordinate(Interpolants, LightMapCoordinate, ShadowMapCoordinate);
	SetLightMapDataIndex(Interpolants, LightMapDataIndex);

#endif // NEEDS_LIGHTMAP_COORDINATE

#if USES_PER_INSTANCE_CUSTOM_DATA
	Result.CustomDataOffset = InstanceData.CustomDataOffset;
	Result.CustomDataCount  = InstanceData.CustomDataCount;
#endif

#if HAS_INSTANCE_LOCAL_TO_WORLD_PS
	Result.InstanceLocalToWorld = InstanceData.LocalToWorld;
#endif
#if HAS_INSTANCE_WORLD_TO_LOCAL_PS
	Result.InstanceWorldToLocal = InstanceData.WorldToLocal;
#endif

	Result.PrimitiveId  = InstanceData.PrimitiveId;
	Result.InstanceId   = InstanceData.RelativeId;

	return Result;
}

// Shared function (for Nanite raster and shading passes) to fetch a valid FMaterialPixelParameters struct, which is used by material inputs.
FMaterialPixelParameters FetchNaniteMaterialPixelParameters(FNaniteView NaniteView, UlongType PackedPixel, bool bHasPageData, FBarycentrics Barycentrics, bool bCalcBarycentrics, uint3 TriIndices, bool bCalcTriIndices, inout FVertexFactoryInterpolantsVSToPS Interpolants, inout float4 SvPosition)
{
	FMaterialPixelParameters Result = (FMaterialPixelParameters)0;

	uint DepthInt = 0;
	uint VisibleClusterIndex = 0;
	uint TriIndex = 0;
	UnpackVisPixel(PackedPixel, DepthInt, VisibleClusterIndex, TriIndex);

	if (VisibleClusterIndex != 0xFFFFFFFF)
	{
		FVisibleCluster VisibleCluster           = GetVisibleCluster(VisibleClusterIndex, bHasPageData);
		FInstanceSceneData InstanceData          = GetInstanceSceneData(VisibleCluster, false);
		FInstanceDynamicData InstanceDynamicData = CalculateInstanceDynamicData(NaniteView, InstanceData);
		FPrimitiveSceneData PrimitiveData        = GetPrimitiveData(InstanceData.PrimitiveId);
		FCluster Cluster                         = GetCluster(VisibleCluster.PageIndex, VisibleCluster.ClusterIndex);

		if (bCalcTriIndices)
		{
			TriIndices = ReadTriangleIndices(Cluster, TriIndex);
		}

		const FNaniteRawAttributeData RawAttributeData = GetRawAttributeData(Cluster, TriIndices, NUM_TEX_COORD_INTERPOLATORS);
		const float3 PointLocalNoWPO[3] = 
		{
			DecodePosition(TriIndices.x, Cluster),
			DecodePosition(TriIndices.y, Cluster),
			DecodePosition(TriIndices.z, Cluster)
		};
		
		const FNaniteVertTransforms Transforms = CalculateNaniteVertexTransforms(InstanceData, InstanceDynamicData, NaniteView);
		const FNaniteTransformedTri Tri = TransformNaniteTriangle(InstanceData, Transforms, PointLocalNoWPO, RawAttributeData);

		if (bCalcBarycentrics)
		{
			// Calculate perspective correct barycentric coordinates with screen derivatives
			const float2 PixelClip = (SvPosition.xy - NaniteView.ViewRect.xy) * NaniteView.ViewSizeAndInvSize.zw * float2(2, -2) + float2(-1, 1);
			Barycentrics = CalculateTriangleBarycentrics(PixelClip, Tri.PointClip[0], Tri.PointClip[1], Tri.PointClip[2], NaniteView.ViewSizeAndInvSize.zw);
		}

		// Update to real depth from VisBuffer
		SvPosition.z = asfloat(DepthInt);

		Result = FetchNaniteMaterialPixelParameters(InstanceData, InstanceDynamicData, NaniteView, Tri, Cluster, Barycentrics, Interpolants, SvPosition);
	}

	return Result;
}

#if IS_NANITE_SHADING_PASS

#if NANITE_USE_HW_BARYCENTRICS
#error NOT_SUPPORTED
#endif

/** Converts from vertex factory specific interpolants to a FMaterialPixelParameters, which is used by material inputs. */
FMaterialPixelParameters GetMaterialPixelParameters(FNaniteView NaniteView, inout FVertexFactoryInterpolantsVSToPS Interpolants, inout float4 SvPosition)
{
	const uint2 PixelPos = SvPosition.xy - float2(0.5f, 0.5f);
	const UlongType PackedPixel = Nanite.VisBuffer64[PixelPos];
	const FBarycentrics Barycentrics = (FBarycentrics)0; // Unused for shading pass (barycentrics are invalid here for full screen tile grid)
	return FetchNaniteMaterialPixelParameters(NaniteView, PackedPixel, VIRTUAL_TEXTURE_TARGET, Barycentrics, true, uint3(0,0,0), true, Interpolants, SvPosition);
}

FMaterialPixelParameters GetMaterialPixelParameters(inout FVertexFactoryInterpolantsVSToPS Interpolants, inout float4 SvPosition)
{
	const FNaniteView NaniteView = GetNaniteView(0);
	return GetMaterialPixelParameters(NaniteView, Interpolants, SvPosition);
}

#endif // IS_NANITE_SHADING_PASS

struct FVertexFactoryRayTracingInterpolants
{
	FVertexFactoryInterpolantsVSToPS InterpolantsVSToPS;
};

float2 VertexFactoryGetRayTracingTextureCoordinate( FVertexFactoryRayTracingInterpolants Interpolants )
{
	return float2(0,0);
}

FVertexFactoryInterpolantsVSToPS VertexFactoryAssignInterpolants(FVertexFactoryRayTracingInterpolants Input)
{
	return Input.InterpolantsVSToPS;
}

FVertexFactoryRayTracingInterpolants VertexFactoryInterpolate(FVertexFactoryRayTracingInterpolants a, float aInterp, FVertexFactoryRayTracingInterpolants b, float bInterp)
{
	return a;
}
