// Copyright 1998-2019 Epic Games, Inc. All Rights Reserved.

/*=============================================================================
	PostProcessDownsample.usf: PostProcessing down sample.
=============================================================================*/

#include "Common.ush"
#include "ScreenPass.ush"

#define DOWNSAMPLE_QUALITY_LOW 0
#define DOWNSAMPLE_QUALITY_HIGH 1

#ifndef DOWNSAMPLE_QUALITY
#error DOWNSAMPLE_QUALITY is not defined.
#endif

Texture2D InputTexture;

SamplerState InputSampler;

SCREEN_PASS_TEXTURE_VIEWPORT(Input)

bool IsComputeUVOutOfBounds(in float2 UV)
{
	float2 CenterDist = abs(UV - 0.5f);
	return (max(CenterDist.x, CenterDist.y) >= 0.5f);
}

float4 SampleInput(float2 UV)
{
	UV = clamp(UV, Input_UVViewportBilinearMin, Input_UVViewportBilinearMax);

	return Texture2DSampleLevel(InputTexture, InputSampler, UV, 0);
}

float4 DownsampleCommon(float2 UV)
{
	float4 OutColor;

#if DOWNSAMPLE_QUALITY == DOWNSAMPLE_QUALITY_LOW
	// Output: low quality, 1 filtered sample
	OutColor = SampleInput(UV);
#elif DOWNSAMPLE_QUALITY == DOWNSAMPLE_QUALITY_HIGH
	// Output: float4(RGBA), 4 filtered samples
	float2 UVs[4];

	// Blur during downsample (4x4 kernel) to get better quality especially for HDR content.
	UVs[0] = UV + Input_ExtentInverse * float2(-1, -1);
	UVs[1] = UV + Input_ExtentInverse * float2( 1, -1);
	UVs[2] = UV + Input_ExtentInverse * float2(-1,  1);
	UVs[3] = UV + Input_ExtentInverse * float2( 1,  1);

	float4 Sample[4];

	UNROLL
	for(uint i = 0; i < 4; ++i)
	{
		Sample[i] = SampleInput(UVs[i]);
	}

	OutColor = (Sample[0] + Sample[1] + Sample[2] + Sample[3]) * 0.25f;

	// Fixed rarely occurring yellow color tint of the whole viewport (certain viewport size, need to investigate more)
	OutColor.rgb = max(float3(0,0,0), OutColor.rgb);
#else
#error Invalid quality level specified.
#endif

	return OutColor;
}

// pixel shader entry point
void MainPS(noperspective float4 InUV : TEXCOORD0, out float4 OutColor : SV_Target0)
{
	OutColor = DownsampleCommon(InUV.xy);
}

#if COMPUTESHADER

SCREEN_PASS_TEXTURE_VIEWPORT(Output)

RWTexture2D<float4> OutComputeTexture;

[numthreads(THREADGROUP_SIZEX, THREADGROUP_SIZEY, 1)]
void MainCS(uint2 DispatchThreadId : SV_DispatchThreadID)
{
	uint2 iPixelPos = DispatchThreadId + Output_ViewportMin;
	float2 PixelPos = float2(iPixelPos);
	float2 UV = (PixelPos + 0.5) * Output_ExtentInverse;

	if (!IsComputeUVOutOfBounds(UV))
	{
		OutComputeTexture[PixelPos] = DownsampleCommon(UV);
	}
}
#endif 
