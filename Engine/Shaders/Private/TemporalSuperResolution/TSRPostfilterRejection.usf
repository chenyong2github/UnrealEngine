// Copyright Epic Games, Inc. All Rights Reserved.

#include "TSRCommon.ush"


//------------------------------------------------------- CONFIG

#define TILE_SIZE 8

#define CONFIG_OUTPUT_HALF_RES DIM_OUTPUT_HALF_RES


//------------------------------------------------------- PARAMETERS

uint4 HistoryRejectionViewport;

Texture2D<tsr_half> HistoryRejectionTexture;
RWTexture2D<tsr_half> HistoryRejectionOutput;


//------------------------------------------------------- LDS

groupshared tsr_half SharedRejection[TILE_SIZE * TILE_SIZE];


//------------------------------------------------------- FUNCTIONS

// Operator that output, lowest, median and highest values from 3 input values.
void LMHOperator(tsr_half A, tsr_half B, tsr_half C, out tsr_half L, out tsr_half M, out tsr_half H)
{
	// TODO: min3, max3, med3.
	tsr_half X = min(B, C);
	tsr_half Y = max(B, C);
	
	L = min(A, X);
	tsr_half Z = max(A, X);

	M = min(Z, Y);
	H = max(Z, Y);
}

// 3 samples median.
tsr_half Median(tsr_half A, tsr_half B, tsr_half C)
{
	// TODO: med3.
	tsr_half L, M, H;
	LMHOperator(A, B, C, L, M, H);
	return M;
}

// 9 samples median using Smith1996.
tsr_half Median9(tsr_half In[9])
{
	// First layer.
	tsr_half L0[3];
	tsr_half M0[3];
	tsr_half H0[3];
	for (uint j = 0; j < 3; j++)
	{
		LMHOperator(In[j * 3 + 0], In[j * 3 + 1], In[j * 3 + 2], L0[j], M0[j], H0[j]);
	}
	
	// Second layer.
	// TODO: min3, max3.
	tsr_half M1[3];
	M1[0] = max(max(L0[0], L0[1]), L0[2]);
	M1[1] = Median(M0[0], M0[1], M0[2]);
	M1[2] = min(min(H0[0], H0[1]), H0[2]);
	
	// Third layer.
	return Median(M1[0], M1[1], M1[2]);
}


//------------------------------------------------------- ENTRY POINT

[numthreads(TILE_SIZE * TILE_SIZE, 1, 1)]
void MainCS(
	uint2 GroupId : SV_GroupID,
	uint GroupThreadIndex : SV_GroupIndex)
{
	float4 Debug = 0.0;

	tsr_short2 RejectionPixelPos = (
		tsr_short2(HistoryRejectionViewport.xy) +
		tsr_short2(GroupId) * tsr_short2(TILE_SIZE, TILE_SIZE) +
		Map8x8Tile2x2Lane(GroupThreadIndex));

	RejectionPixelPos = ClampPixelOffset(RejectionPixelPos, HistoryRejectionViewport.xy, HistoryRejectionViewport.zw - 1);

	// Fetch neighborhood of the kernel
	tsr_half InSamples[9];
	UNROLL
	for (uint i = 0; i < 9; i++)
	{
		tsr_short2 SampleInputPixelPos = AddAndClampPixelOffset(
			RejectionPixelPos,
			tsr_short2(kOffsets3x3[i]), tsr_short2(kOffsets3x3[i]),
			HistoryRejectionViewport.xy, HistoryRejectionViewport.zw - 1);

		InSamples[i] = HistoryRejectionTexture[SampleInputPixelPos];
	}

	tsr_half Rejection = Median9(InSamples);

	#if CONFIG_OUTPUT_HALF_RES
	{
		SharedRejection[GroupThreadIndex] = Rejection;

		GroupMemoryBarrierWithGroupSync();

		UNROLL
		for (uint i = 1; i < 4; i++)
		{
			uint ButterflySwap = i;

			Rejection = min(Rejection, SharedRejection[GroupThreadIndex ^ ButterflySwap]);
		}
		
		bool bIsValid = all(RejectionPixelPos <= tsr_short2(HistoryRejectionViewport.zw)) && all((tsr_ushort2(RejectionPixelPos) % tsr_ushort(2)) == tsr_ushort(0));

		tsr_ushort2 OutputPixelPos = tsr_ushort2(RejectionPixelPos) / tsr_ushort(2);
		OutputPixelPos.x = bIsValid ? OutputPixelPos.x : ~tsr_ushort(0);

		HistoryRejectionOutput[OutputPixelPos] = Rejection;
	}
	#else
	{
		tsr_short2 OutputPixelPos = InvalidateOutputPixelPos(RejectionPixelPos, HistoryRejectionViewport.zw);
		HistoryRejectionOutput[OutputPixelPos] = Rejection;
	}
	#endif
	
	#if DEBUG_OUTPUT
	{
		tsr_short2 OutputPixelPos = InvalidateOutputPixelPos(RejectionPixelPos, HistoryRejectionViewport.zw);
		DebugOutput[tsr_short3(OutputPixelPos, 0)] = Debug;
	}
	#endif
}
