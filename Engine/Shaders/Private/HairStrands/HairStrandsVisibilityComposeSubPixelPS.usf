// Copyright 1998-2019 Epic Games, Inc. All Rights Reserved.

#include "../Common.ush"

#include "../SceneTexturesCommon.ush"
#include "HairStrandsDeepShadowCommon.ush"
#include "../DeferredShadingCommon.ush"
#include "HairStrandsVisibilityCommon.ush"

Texture2D<float4> SubPixelColorTexture;
#if PERMUTATION_PPLL
Texture2D<uint4> CategorisationTexture;
#else
Texture2DMS<float, HAIR_VISIBILITY_MAXSAMPLECOUNT> HairVisibilityDepthTexture;
#endif

void SubColorPS(
	in FScreenVertexOutput Input,
	out float4 OutColor  : SV_Target0
	, out float  OutDepth : SV_Depth
)
{
	const uint2 PixelCoord = floor(Input.Position.xy);
	OutColor = SubPixelColorTexture.Load(uint3(PixelCoord, 0));

	// Early out writing to not write stencil value for full coverred pixels (or not covered at all)
	//if (OutColor.a == 0)
	//	discard;

#if PERMUTATION_PPLL

	const FCategorizationData CategorisationData = DecodeCategorizationData(CategorisationTexture.Load(uint3(Input.Position.xy, 0)));
	OutDepth = CategorisationData.ClosestDepth;

#else

	// Brute force reading of the depth value. This could be pre-computed during the categorize pass or the compaction pass
	// This must match what is done in VisibilityDepthPS to fonction. TODO: do not write depth here as it is already in. 
	OutDepth = 0;
	for (uint SampleIt = 0; SampleIt < HAIR_VISIBILITY_MAXSAMPLECOUNT; ++SampleIt)
	{
		const float SampleDepth = HairVisibilityDepthTexture.Load(PixelCoord, SampleIt);
		OutDepth = max(OutDepth, SampleDepth); // HAS_INVERTED_Z_BUFFER
	}

#endif
}

float VelocityThreshold;
Texture2DMS<float2, HAIR_VISIBILITY_MAXSAMPLECOUNT> HairVisibilityVelocityTexture;

void FastResolvePS(in FScreenVertexOutput Input)
{
	const uint2 PixelCoord = floor(Input.Position.xy);

	bool bNeedFastResolve = false;
	for (uint SampleIt = 0; SampleIt < HAIR_VISIBILITY_MAXSAMPLECOUNT; ++SampleIt)
	{
		const float2 EncodedVelocity = HairVisibilityVelocityTexture.Load(PixelCoord, SampleIt);
		const float2 Velocity = DecodeVelocityFromTexture(EncodedVelocity);
		const float VelocityMagnitude = sqrt(dot(Velocity, Velocity));
		if (EncodedVelocity.x > 0 && VelocityMagnitude > VelocityThreshold)
		{
			bNeedFastResolve = true;
			break;
		}
	}

	if (!bNeedFastResolve)
		discard;
}