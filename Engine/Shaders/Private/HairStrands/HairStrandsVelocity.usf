// Copyright 1998-2019 Epic Games, Inc. All Rights Reserved.

#pragma once

#include "../Common.ush"
#include "HairStrandsVisibilityCommon.ush"

#define RENDER_MODE_MSAA 0
#define VENDOR_AMD 0
#define VENDOR_NVIDIA 1
#define VENDOR_OTHER 2

#if PERMUTATION_VENDOR == VENDOR_AMD
#define TILE_PIXEL_SIZE_X 8
#define TILE_PIXEL_SIZE_Y 8
#elif PERMUTATION_VENDOR == VENDOR_NVIDIA
#define TILE_PIXEL_SIZE_X 8
#define TILE_PIXEL_SIZE_Y 4
#else //VENDOR_OTHER
#define TILE_PIXEL_SIZE_X 8
#define TILE_PIXEL_SIZE_Y 8
#endif

uint2 ResolutionOffset;

Texture2D<uint> NodeIndex;
Buffer<float2> NodeVelocity;
StructuredBuffer<FNodeVis> NodeVis; 

#define VELOCITY_TYPE_NONE 0
#define VELOCITY_TYPE_AVG 1  
#define VELOCITY_TYPE_CLOSEST 2
#define VELOCITY_TYPE_MAX 3
#if PERMUTATION_VELOCITY != VELOCITY_TYPE_NONE
	RWTexture2D<float2> OutVelocityTexture;
#endif

[numthreads(TILE_PIXEL_SIZE_X, TILE_PIXEL_SIZE_Y, 1)]
void MainCS(uint GroupIndex : SV_GroupIndex, uint3 DispatchThreadId : SV_DispatchThreadID)
{
	const uint2 PixelCoord = DispatchThreadId.xy + ResolutionOffset;
	const FNodeDesc NodeDesc = DecodeNodeDesc(NodeIndex.Load(uint3(PixelCoord, 0)));
	if (NodeDesc.Count > 0)
	{
		// Store final sort node data 
		#if PERMUTATION_VELOCITY == VELOCITY_TYPE_AVG
		float2 AverageVelocity = 0;
		#endif

		#if PERMUTATION_VELOCITY == VELOCITY_TYPE_CLOSEST
		float2 ClosestEncodedVelocity = 0;
		float ClosestDepth = 0; // Inverse-Z
		#endif		

		#if PERMUTATION_VELOCITY == VELOCITY_TYPE_MAX
		float2 MaxEncodedVelocity = 0;
		float MaxVelocityMagnitude2 = 0;
		#endif		

		for (uint NodeIt = 0; NodeIt < NodeDesc.Count; ++NodeIt)
		{
			const uint SampleIndex = NodeDesc.Offset + NodeIt;
			const float2 EncodedVelocity = NodeVelocity[SampleIndex];

			#if PERMUTATION_VELOCITY == VELOCITY_TYPE_AVG
			AverageVelocity += DecodeVelocityFromTexture(EncodedVelocity);
			#endif			

			#if PERMUTATION_VELOCITY == VELOCITY_TYPE_CLOSEST
			const float NodeDepth = NodeVis[SampleIndex].Depth;
			if (NodeDepth > ClosestDepth) // Inverse-Z
			{
				ClosestEncodedVelocity = EncodedVelocity;
				ClosestDepth = NodeDepth;
			}
			#endif

			#if PERMUTATION_VELOCITY == VELOCITY_TYPE_MAX
			const float2 CurrentVelocity = DecodeVelocityFromTexture(EncodedVelocity);
			const float CurrentVelocityMagnitude2 = dot(CurrentVelocity, CurrentVelocity);
			if (CurrentVelocityMagnitude2 > MaxVelocityMagnitude2)
			{
				MaxEncodedVelocity = EncodedVelocity;
				MaxVelocityMagnitude2 = CurrentVelocityMagnitude2;
			}
			#endif			
		}

		#if PERMUTATION_VELOCITY == VELOCITY_TYPE_AVG
		OutVelocityTexture[PixelCoord] = EncodeVelocityToTexture(AverageVelocity / max(NodeDesc.Count, 1));
		#endif		

		#if PERMUTATION_VELOCITY == VELOCITY_TYPE_CLOSEST
		OutVelocityTexture[PixelCoord] = ClosestEncodedVelocity;
		#endif		

		#if PERMUTATION_VELOCITY == VELOCITY_TYPE_MAX
		OutVelocityTexture[PixelCoord] = MaxEncodedVelocity;
		#endif		
	}
}
